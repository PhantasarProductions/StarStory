// License Information:
// **********************************************
// 
// src/Tricky Script/Script/Use/Anyway.NeilBundle/Aurina.neil
// (c) Jeroen Broks, 2021, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 21.03.17
// End License Information



module

	void ChaChing()
		static var ChaChingSound = Audio.LoadNew("Audio/Shopping/ChaChing.ogg","CHACHING")
		ChaChingSound.Play()
	end

	readonly var G = Lua.Neil.Globals
	
	get int Have
		return G.gvInt.Aurinas
	end
	
	set int Have
		if value>G.gvInt.Aurinas && math.random(1,5*__skill)==1
			g.gvint.AurinaRate++
		end			
		G.gvInt.Aurinas = value
	end
	
	get int Rate
		switch G.gvInt.Skill
			case 1
				g.gvint.AurinaRate = math.max(500,g.gvint.AurinaRate)
			case 2
				g.gvint.AurinaRate = math.max(100,g.gvint.AurinaRate)
			case 3
				g.gvint.AurinaRate = math.max(5,g.gvint.AurinaRate)
			default
				Lua.error("Aurina rate skill fuck up error!")
		end
		return g.gvint.AurinaRate
	end
	
	set int Rate
		G.gvInt.AurinaRate = math.min(value,g.caps.maxaurinarate)
	end
	
	get int AurinaCredits
		return Rate*Have
	end
	
	get int Cash
		return g.gvInt.Cash
	end
	
	set int Cash
		g.gvInt.Cash = math.min(G.Caps.MaxMoney,value)
	end
	
	void Verzilver()
		// Needed for the boxtext!		
		g.gvStr.AURINAS           = sprintf("%4d",Have)
		g.gvStr.AURINARATE        = sprintf("%4d",Rate)
		g.gvStr.AURINACREDITS     = sprintf("%6d",AurinaCredits)
		g.gvInt.AURINASEXCHANGED += Have
		Cash += AurinaCredits		
		Have = 0
		ChaChing()
	end
	
	get int Spent4Rate
		return g.gvint.AurinaSpent4rate
	end
	
	set int Spent4Rate
		static int rinc = 100*(10^__skill)
		g.gvint.AurinaSpent4rate = value
		while g.gvint.AurinaSpent4rate>rinc
			g.gvint.AurinaSpent4rate-=rinc
			Rate ++
		end
	end
	
	get int Earned4Rate
		return g.gvint.AurinaEarned4rate
	end

	set int Earned4Rate
		static int rinc = 123*(10^__skill)
		g.gvint.AurinaEarned4rate = value
		while g.gvint.AurinaEarned4rate>rinc
			g.gvint.AurinaEarned4rate-=rinc
			Rate ++
		end
	end

	
	bool Pay(int amount)
		if amount<0
			Lua.error("Negative payment requested!")
		elseif amount>Cash
			return false
		end
		cash-=amount
		Spent4Rate+=amount
		g.gv.gint.TotalCashSpent+=amount
		ChaChing()
		return true
	end
	
	void Earn(int amount)
		if amount<0
			Lua.error("Negative earning requested!")
		end
		cash+=amount
		Earned4Rate+=amount
		ChaChing()
	end
	
	get int Shilders
		return g.gv.gint.Shilders
	end
	set int Shilders
		g.gv.gint.Shilders=value
	end
	
end