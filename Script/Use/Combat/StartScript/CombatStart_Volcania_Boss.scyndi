// License:
// **********************************************
// 
// src/Script/Script/Use/Combat/StartScript/CombatStart_Volcania_Boss.scyndi
// (c) Jeroen Broks, 2021, 2025, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// 
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// 
// version: 25.02.01 X
// 
// End License


Module

#use "Libs/SCI/Sys"
#use "Libs/SCI/Events"
#use "Libs/SCI/Graphics"
#use "Libs/SCI/InterVar"

#use "Script/Use/Link"
#use "Script/Use/Anyway"

#use "Script/Use/Convert/Flow"
#use "Script/use/Convert/RPG2Stat"


locExtern Combat_Fighters "Scyndi.Class.Combat_Fighter"
locExtern Combat_Fighter "Scyndi.Class.Combat_Fighter"
locExtern Combat_Time "Scyndi.Class.Combat_Time"
locExtern Combat_Draw "Scyndi.Class.Combat_Draw"
locExtern Screen "Scyndi.Class.Graphics"

plua ILight = LoadNewImage("GFX/Combat/SpellAni/Thunder/Lightning.png","SPELLANI_Lightning")
plua Wendicka = Combat_Fighter.Fighters.Wendicka


Void BliksemWendicka()
	int y := Wendicka.Y	
	number sh := y / ILight.H 
	int a=math.random(50,100)
	ILight.HotBottomCenter()
	for i=1,a
		int x := math.random(Wendicka.x - Wendicka.w,Wendicka.x + Wendicka.w)
		number sw = math.random(100,1000)/750
		Graphics.Cls()
		Combat_Draw.DrawScreen()		
		__white
		Graphics.Scale(sw,sh)
		ILight.Draw(x,y)
		Graphics.Scale(1,1)
		Graphics.Flip()
		//Sys.WaitMinTicks()
		Events.Poll()
	end
end
		

Void KillWendicka()
	delegate rand = math.random
	If RPGChar.Wendicka.Points.HP.Have>1
		Return
	End
	RPGChar.Wendicka.Points.HP.Have = 1 // prevent 'wipe out' event
	// Count the kill
	gint.KILLED_SUPERBOSS_FIRESPIDER++
	// Start
	BliksemWendicka()
	MapText("RELEASE_A","FLOW_COMBAT")
	BliksemWendicka()
	MapText("RELEASE_B","FLOW_COMBAT")
	BliksemWendicka()
	// I want to break free
	var Goddess = LoadImage("GFX/SPECIAL/GODDESS.png","VOLCANIA_GODDESS") //,"GODDESS"); 
	Goddess.HotCenter("GODDESS")
	number alpha = 0
	number alphadir = 0.2
	number scale = 0
	repeat                                                                           
		Graphics.Color(0,math.random(100,180),math.random(180,255))                                          
		//Image.Draw("TMP_COMBATSCREEN",0,0)	
		for i=0,Screen.H div 2     
			Graphics.Color(0,rand(100,180),rand(180,255)) 
			Graphics.Line(Screen.W div 2,Screen.H div 2,  0,rand(0,Screen.H )) 
			Graphics.Line(Screen.W div 2,Screen.H div 2,Screen.W ,rand(0,Screen.H )) 
			Graphics.Line(Screen.W div 2,Screen.H div 2,rand(0,Screen.W ),  0)
			Graphics.Line(Screen.W div 2,Screen.H div 2,rand(0,Screen.W ),Screen.H )
		end
		Graphics.Scale(scale,scale) 
		Graphics.SetAlpha(math.floor(alpha))
		Goddess.Draw(Screen.W div 2,Screen.H div 2) //Image.Draw("GODDESS",Screen.W div 2,Screen.H div 2)                                                        
		scale=scale+0.005
		alpha=alpha+alphadir
		if alpha>=255 || scale * Goddess.Width > Screen.W+50
    		alphadir=-0.4 
    	end              
    	Graphics.Scale(1,1)
    	Graphics.SetAlpha(255)
    	Graphics.Flip()   
    	Events.Poll()
    	//FPS.WaitMinTicks()
    until alpha<=0                                                                       	
	// End combat
	Goddess.Kill()
	gBool.WendickaGoddessReleased = true
	gbool.WendickaDream = true
	Flow.GoToFlow("FIELD")
	End

Void HurtMonster()
	Var M := Combat_Fighter.Fighters.FOE_0.Char
	CSayF("Let's see: %d/%d",M.Points.HP.Have,M.Points.HP.Maximum)
	If M.Points.HP.Have<=1
		//M.Points.HP.Maximum++
		M.Stats.BASE_HP++
	End
	While M.Points.HP.Have < M.Points.HP.Maximum
		If M.Points.HP.Have<Caps.Cycle+__skill && M.Points.HP.Maximum<100
			//M.Points.HP.Maximum++
			M.Stats.BASE_HP++
		End
		M.Points.HP.Have++
		M.Stats.BASE_Strength += math.random(8,math.max(10,math.min(Caps.Cycle,25)*4))
		M.Stats.BASE_Agility  += math.random(1,30)
		M.Stats.BASE_Accuracy += 4+Caps.Cycle
		// CSayF("Up! %d/%d",M.Points.HP.Have,M.Points.HP.Maximum)
	End
	CSay("Monster HP set back to max")
	CSayF("Strength of monster set to %005d",M.Stats.END_Strength)
	CSayF("Agility of monster set to %005d",M.Stats.END_Agility)
	CSayF("Accuracy of monster set to %004d",M.Stats.END_Accuracy)
	Combat_Time.CalcHighSpeed()
End

Void BlaBlaBla()
	Var Wendicka = Combat_Fighter.Fighters.Wendicka
	Var Monster = Combat_Fighter.Fighters.FOE_0
	Monster.Char.Stats.BASE_HP = 5 + Caps.Cycle
	Monster.Char.Points.HP.Have = Monster.Char.Points.HP.Maximum 
	Wendicka.HurtResponse=KillWendicka
	//Monster.Char.Points.HP.Minimum = Monster.Char.Points.HP.Maximum
	Monster.AltX = (Screen.W div 4)
	Monster.AltY = (Screen.H div 4) * 3
	Monster.HurtResponse=HurtMonster
End

Void StartScript()
	BlaBlaBla()
End

//Return BlaBlaBla
