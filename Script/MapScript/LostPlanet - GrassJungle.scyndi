// License:
// **********************************************
// 
// src/Script/Script/MapScript/LostPlanet - GrassJungle.scyndi
// (c) Jeroen Broks, 2021, 2025, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// 
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// 
// version: 25.01.27 IV
// 
// End License

Script

#use "Libs/SCI/Sys"
#use "Libs/SCI/Events"
#use "Libs/SCI/Kthura"
#use "Libs/SCI/Graphics"
#use "Libs/SCI/InterVar"

#use "Script/Use/Link"
#use "Script/Use/Anyway"

//#MKL_Version "Star Story - LostPlanet - GrassJungle.neil" "21.03.03"
//#MKL_Lic     "Star Story - LostPlanet - GrassJungle.neil" "Phantasar Closed License"

group MScript

	Void Arrival()
		for c in each { [0]="PLAYER","Crystal","ExHuRU" }
			string Tex = c
			if c=="PLAYER"
				Map.SpawnPlayer("Start_Wendicka")
				Tex="Wendicka"
			else
				Map.Spawn(c,"Start_"..c)
			end
			Map.SetObj(c,"Texture","GFX/Actors/Player/"..Tex..".East.jpbf")
		end
		MapText("START")
		for c in each { [0] = "Crystal","ExHuRU" }
			Map.Kill(c)
		end
		Spawn.Reset()
	end
	
	Void TutorPad()
		if ! Done.Tutorial_TransporterPads
			CSay("OP DE PLAATS, RUST!")
			Player.StopWalking()
			CSay("To the transporter! NOW!")
			Player.WalkTo("Trans.Spot.GRASSJUNGLE_CRASH")
			CSay("Explain!")
			MapText("TUT_PAD")
			CSay("Activate!")
			Map.ActTrans("GRASSJUNGLE_CRASH")
			CSay("Done!")
		end
		if gbool.MeetYornoAtCrash && (! Kthura.HasObj("Yorno") )
			// (240,2143)
			yorno:=Kthura.NewObstacle(240, 2140, "GFX/Actors/SinglePic/Random/Yorno.png")
			yorno.Tag="Yorno"
			Kthura.Remap()
			Sys.Call("FLOW_FIELD","Clickable.Create","Yorno","#001",240,2170,"Yorno",nil)
		end
	end
	
	void Yorno()
		if !Done.AllowMedalDungeons
			MapText("YORNO")
		else
			MapText("YORNO2")
		end
	end
	
	Void WeGaanNaar2()
		Map.Load("LostPlanet - GrassJungle_2")
		Schedule.Add("SPAWN:Start")
	End
	
	Void HideSecret()
		CSay("Hide the secret area")
		Map.HideByLabel("Secret")
	end
	
	Void ShowSecret()
		CSay("Secret passage touched")
		Map.ShowbyLabel("Secret")
	end
	
	void ToSecretDungeon()
		Map.Load("LostPlanet - Bonus_Dark_Caves_of_Shilington")
		Schedule.Add("SPAWN:Start")
	end
	
	void ToFloor3()
		Map.Load("LostPlanet - GrassJungle_3")
		Schedule.add("SPAWN:Start")
	end
	
	void ToFloor1()
		Map.Load("LostPlanet - GrassJungle")
		Schedule.add("SPAWN:Einde")
	end

	void BackToFloor2()
		Map.Load("LostPlanet - GrassJungle_2")
		Schedule.add("SPAWN:Einde")
	end
	
	void Boss()
		Int BossLevel
		switch Caps.Cycle
			case 0 1
				BossLevel = 15 + (__skill*5)
			case 2 
				BossLevel = 190 + (__skill*10)
			case 3
				BossLevel = 400 + (__skill*25)
			case 4
				BossLevel = 1000 + (__skill*50)
			case 5 6
				Bosslevel = 5000 + (__skill*100)
			case 7 8
				BossLevel = 3000 + (__skill*250)
			case 9
				BossLevel = 5500 + (__skill*500)
			case 10
				BossLevel = 7000
			case 11
				BossLevel = 8000
			case 12
				BossLevel = 9000
			case 13
				BossLevel = 9500
			case 14
				BossLevel = 9800
			default
				BossLevel = 10000
		end
		StartCombat.Foe("Boss/BigGrassTiger",BossLevel)
	end
	
	void BossTutorial()
		MapText("BOSS."..Upper(Map.Leader))
		MapText("BOSS.TUTORIAL")
		Map.Kill("BossTutorial",true)
	end
	
	void EndJump()
		number X = Player.X
		number Y = Player.Y
		Player.StopWalking()
		repeat
			Events.Poll()
			Y++
			if X<416
				x+=0.5
			elseif x>416
				x-=0.5
			end
			Player.X = math.floor(x)
			Player.Y = y
			Graphics.Cls()
			Map.DrawScreen()
			Graphics.Flip()
			Sys.WaitMinTicks()
		until y>450
		Player.WalkTo("AfterJump")
	end

	VOID Pub()
		Map.Load("LostPlanet - BarOutside")
		Schedule.Add("SPAWN:Start")
	END
	
	void Update()
	end

	void Draw()
	end
	
	void Load()
	end
end	

Init
	ZA.Enter("Pad_Tutorial","TutorPad")
	for _,z in pairs {"WeGaanNaar2","TOSECRETDUNGEON","ToFloor3","ToFloo1","BackTo2","BossTutorial","Pub"}
		ZA.Enter(z,z)
	end
	for i=1,3
		ZA.Enter("HIDE_SECRET"..i,"HideSecret")
	end
	ZA.Enter("REVEAL_SECRET","ShowSecret")
	ZA.Enter("JumpEast","EndJump")
	ZA.Enter("JumpWest","EndJump")
end


