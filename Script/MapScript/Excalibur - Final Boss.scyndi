// License:
// **********************************************
// 
// src/Script/Script/MapScript/Excalibur - Final Boss.scyndi
// (c) Jeroen Broks, 2021, 2025, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// 
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// 
// version: 25.02.12 VII
// 
// End License
Script

#define Fight_Goddess

#use "Libs/SCI/Sys"
#use "Libs/SCI/Graphics"
#use "Libs/SCI/InterVar"

#use "libs/Music"

#use "Script/use/Link"
#use "Script/Use/Anyway"
#use "Script/Use/Convert/RPG2Stat"
#use "script/Use/Map/KickReggieUse"


//#MKL_Version "Star Story - Excalibur - Final Boss.neil" "21.05.18"
//#MKL_Lic     "Star Story - Excalibur - Final Boss.neil" "Phantasar Closed License"

class SchuifDeur
	static table Mapje = {}
	
	readonly int prevLinks
	readonly int prevRechts
	readonly int nextLinks
	readonly int nextRechts
	static bool PrevOpen
	static bool NextOpen
	int NextSchuif
	int PrevSchuif
	
	Constructor
		for d in each{[0]="PrevLinks","PrevRechts","NextLinks","NextRechts"}
			if Map.HasTag(d)
				self[d]=Map.ObjInt(d,"X")
				CSayF("Layer %s: Schuifdeur: %s => x = %04d",Map.Layer,d,self[d])
			else
				self[d]=-1
				CSayF("Layer %s: Schuifdeur: %s => Non-Existent!",Map.Layer,d)
			end
		end
	end
	
	static void Check(string OL,string CL)
		Mapje[CL] = Mapje[CL] || new SchuifDeur()
		Mapje[CL].CheckFl(OL,CL)
	end
	
	void CheckFL(string OL, string CL)
		if PrevOpen 
			if OL!=CL
				PrevSchuif=30
			elseif PrevSchuif<30
				PrevSchuif++
			end
		else
			if OL!=CL
				PrevSchuif=0
			elseif PrevSchuif>0
				PrevSchuif--
			end
		end
		if PrevLinks>=0
			Map.SetObj("PrevLinks","X",PrevLinks-PrevSchuif)
			Map.SetObj("PrevRechts","X",PrevRechts+PrevSchuif)
		end
		if NextOpen 
			if OL!=CL
				NextSchuif=30
			elseif NextSchuif<30
				NextSchuif++
			end
		else
			if OL!=CL
				NextSchuif=0
			elseif NextSchuif>0
				NextSchuif--
			end
		end
		if NextLinks>=0
			Map.SetObj("NextLinks","X",NextLinks-NextSchuif)
			Map.SetObj("NextRechts","X",NextRechts+NextSchuif)
		end
	end
	
	void Debug()
	#if DEBUG_SCHUIFDEUR
		static var DFnt := ObtainFont("BOXTEXT")
		dFnt.DarkText(sprintf("Prev Open: %s",lua.tostring(PrevOpen)),5,0)
		dFnt.DarkText(sprintf("Prev Corr: %04d",PrevSchuif),5,30)
		dFnt.DarkText(sprintf("Next Open: %s",lua.tostring(NextOpen)),5,60)
		dFnt.DarkText(sprintf("Next Corr: %04d",NextSchuif),5,90)
	#fi
	end		
end
	


group MScript

	void SchuifPrev()
		CSayF("PREV: Sesam open u")
		SchuifDeur.PrevOpen = true
	end
	void Leave_SchuifPrev()
		CSayF("PREV: En ga maar weer dicht")
		SchuifDeur.PrevOpen = false
	end
	void SchuifNext()
		CSayF("NEXT: Sesam open u")
		SchuifDeur.NextOpen = true
	end
	void Leave_SchuifNext()
		CSayF("NEXT: En ga maar weer dicht")
		SchuifDeur.NextOpen = false
	end
	
	Void DIE_Lovejoy()
		If !Done.Lovejoy_is_dead			
			Music.Play("Music/Special/Wiped.ogg")
			PPop.Pop("LJ","North")
			MapText("LOVEJOY")
			Party.GiveEXP(1234)
			PPop.UnPop()
			// Crash prevention?
			Map.SetObj("PLAYER", "Texture", "GFX/Actors/Uniform/Wendicka.North.jpbf")
		End
	End
	
	Void NPC_LoveJoy()
		MapTextLeader("LOVEJOY.DEAD")
	End

	Void Goddess_Room()
		Music.Play("Music/Sys/Silence.ogg")
		If !Done.Entered_Room_Of_The_Goddess
			Party.GiveEXP(1234)
		End
		Map.HideObj("Reggie")
	End
	
	Void NPC_Goddess()
		Map.ShowObj("Reggie")
		PPop.Pop("Goddess","North")
		MapText("GODDESS1")
		KickReggie("PartyPopped_Foxy","Reggie","West")
		MapText("GODDESS2")
		Schedule.Add("Post_Goddess")		
		#if Fight_Goddess
			//Lua.error("Goddess fight not yet scripted")
			Int Lv
			Do
				Int TLv = 0
				For ch In each{"Wendicka","Crystal","Yirl","Foxy","Xenobi","Johnson"}
					TLv += RPGChar[ch].Stat.Level
				End
				Lv = TLv Div (8 - __skill)
			End
			StartCombat.Prepare()
			StartCombat.Foe("Goddess/Goddess",Lv)
			Table Adds = {[0]="Add_Gunner","Add_FlameThrower","Add_IceCannon","Add_WaterGun","Add_WindGun","Add_RockThrower","Add_LightGun","Add_DarkGun","Add_VenomGun","Add_virusGun","Add_Trq","Add_BlockGun","Add_ConfuseGun"}
  			Table Got = {}
  			Table LvMargin = {0.50,0.25,0.10}
  			int maxadds = (3*__skill)
			For i=1,maxadds
				CSayF("Adding add: %d/%d",i,maxadds)
				String Picked
				Int TimeOut = 1000
				Repeat
					Picked = Adds[math.random(0,len(Adds)-1)]
					TimeOut --
					Lua.assert(TimeOut>0,"Goddess Add Time-Out")
				LoopWhile Got[Picked]
				Got[Picked]=True
				StartCombat.Foe("Goddess/"..Picked,math.random(math.floor(lv-(lv*LvMargin[__skill])),lv))
			End
			StartCombat.Tune = "Music/Combat/SpecialBoss/Spellbound.ogg"
			StartCombat.Boss = True
			StartCombat.Arena = "Orionnevel.png"
			StartCombat.Begin()
		#fi
	End
	
	Void Post_Goddess()
		//Player.Y -= 40
		Map.SetObj("PartyPopped_Wendicka","Y",Map.ObjInt("PartyPopped_Wendicka","Y")-40)
		Map.SetObj("PartyPopped_Wendicka","TEXTURE","GFX/Actors/Player/Wendicka.Hose.png")
		MapText("POSTBOSS1")
		Map.SetObj("PartyPopped_Wendicka","TEXTURE","GFX/Actors/Player/Wendicka.Hose.Water.png")
		MapText("POSTBOSS2")
		Map.Kill("NPC_Goddess",true)
		Map.SetObj("PartyPopped_Wendicka","TEXTURE","GFX/Actors/Player/Wendicka.Hose.png")
		Music.Pop()
		MapText("POSTBOSS3")
		Map.HideButLabel("ONTRIAL")
		MapText("TRIAL")
		Map.GoToMap("Excalibur - Post Game","POSTGAME","Start")
		For i,ch In iPairs {"Wendicka","Crystal","Foxy","Yirl","Xenobi",""}
			RPGParty[i]=ch
		End
		Map.Leader="Wendicka"
		//Lua.error("No post-boss script yet!")
	End
	
	Void Update()
		static string OldLayer = "Okay, laten we starten!"
		string Layer = Map.Layer
		SchuifDeur.Check(OldLayer,Layer)
		OldLayer = Layer
	End

	Void Draw()
	End
	
	Void Load()
	End

End

Init
	Map.AllowQuitSave=false
	ZA.TabEnter{"SchuifPrev","SchuifNext","DIE_Lovejoy","Goddess_Room"}
	ZA.TabLeave{"SchuifPrev","SchuifNext"}
End

