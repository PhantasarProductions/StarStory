// License Information:
// **********************************************
// 
// src/Tricky Script/Script/MapScript/Excalibur - UnderAttack.neil
// (c) Jeroen Broks, 2021, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 21.02.20
// End License Information

#use "Script/Use/Anyway"

#MKL_Version "Star Story - Excalibur - UnderAttack.neil" "21.02.20"
#MKL_Lic     "Star Story - Excalibur - UnderAttack.neil" "Phantasar Closed License"

#macro StartCyborgsDead gv.gbool.Excalibur_StartCyborgsDead
#macro ShowAll gv.gbool.Excalibur_Broken_Transporter_Tried

group MScript
	
	get int Cycle
		return Caps.Cycle
	end

	void Update()
	end

	void Draw()
	end
	
	void Load()
	end
	
	void What2Show()
		if !ShowAll
			Map.HideButLabel("Galahad")
		end
		Map.ShowButLabel("TroepDieTochNietBestaat")
	end
	
	void Start()
		What2Show()
		Map.SpawnPlayer("Start")
		Player.Wind="South"
		if !StartCyborgsDead
			MapText("WELCOME")
			Schedule.Add("Killed_StartCyborgs")
			StartCombat.Prepare()
			StartCombat.Foe("Reg/Cyborg Medic",math.min(10000,math.random(__skill+3,(4*__skill)+1))+((Cycle-1)*100))
			StartCombat.Foe("Reg/Cyborg Captain",math.min(10000,math.random(__skill+5,(6*__skill)+2)+((Cycle-1)*100)))
			StartCombat.Foe("Reg/Cyborg Gunner",math.min(10000,math.random(__skill+3,(4*__skill)+1))+((Cycle-1)*100))
			StartCombat.Tune = StartCombat.RandomTune
			StartCombat.Boss = false
			StartCombat.Arena = "Excalibur.png"	
			StartCombat.Begin()			
		end
	end
	
	void GoHome()
		Map.Load("Excalibur - Home")
		Schedule.Add("Binnenkomst")
	end
	
	Void Killed_StartCyborgs()
		for i=1,3
			Map.Kill("Cyborg"..i,true)
		end
		MapText("MORGUE")
		StartCyborgsDead = true
	end
	
	Void BrokenTransporter()
		Map.Kill("BrokenTransporter",true)
		Map.Kill("Block4Transporter",true)
		Map.Kill("Check_Transporter_First",true)
		MapText("NoTransport")
		ShowAll=true
		What2Show()
	End
		
	Void Guinevere()
		MapText("GUINEVERE."..(Map.Leader:upper()))
	end

	Void Lancelot()
		MapText("LANCELOT."..(Map.Leader:upper()))
	end

	Void CheckTransporterFirst()
		MapText("TRANSPORTERFIRST."..(Map.Leader:upper()))
	end
	
	Void Secret()
		Map.Leader = "Wendicka"
		Map.Spawn("Crystal","PLAYER")
		Map.Spawn("ExHuRU","PLAYER")
		Map.SetObj("Crystal","Texture","GFX/Actors/Player/Crystal.North.jpbf")
		Map.SetObj("ExHuRU","Texture","GFX/Actors/Player/ExHuRU.North.jpbf")
		Player.WalkTo("Secret_Wendicka")
		for i=1,100
			if i==30
				Map.ObjWalkTo("Crystal","Secret_Crystal")
			elseif i==60
				Map.ObjWalkTo("ExHuRU","Secret_ExHuRU")
			end
			__white
			Graphics.Cls()
			Map.DrawScreen()
			Graphics.Flip(25)
		end
		MapText("SECRET")
		for i=0,40
			Map.SetObj("Geheim_Links" ,"X",3008-i)
			Map.SetObj("Geheim_Rechts","X",3040+i)
			Graphics.Cls()
			Map.DrawScreen()
			Graphics.Flip(25)
		end
		Map.Remap()
		MapText("SECRET.2")
		for a in each { "PLAYER","Crystal", "ExHuRU" }
			Map.ObjWalkTo(a,"EnWeg")
		end
		for i=100,0,-1
			if i<40
				Map.SetObj("Geheim_Links" ,"X",3008-i)
				Map.SetObj("Geheim_Rechts","X",3040+i)
			end
			Graphics.Cls()
			Map.DrawScreen()
			Graphics.Flip(25)
		end
		Map.Load("Excalibur - SecretPassage")
		Schedule.Add("Start")
	end
	
	CONSTRUCTOR
		ZA.Enter("Go Home","GoHome")
		ZA.Enter("BrokenTransporter","BrokenTransporter")
		ZA.Enter("Guinevere","Guinevere")
		ZA.Enter("Lancelot","Lancelot")		
		ZA.Enter("Check_Transporter_First","CheckTransporterFirst")
		ZA.Enter("Transporter_Gawain","Secret")
	end

end