// License Information:
// **********************************************
// 
// src/Tricky Script/Script/MapScript/Hawk.neil
// (c) Jeroen Broks, 2021, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 21.04.10
// End License Information

#use "Script/Use/Anyway"
#use "Script/Use/Map/KickReggie"

#MKL_Version "Star Story - Hawk.neil" "21.04.10"
#MKL_Lic     "Star Story - Hawk.neil" "Phantasar Closed License"

#macro __inf a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,q,r,s,t,u,v,w,x,y,z

var InPartyChecked
QuickMeta InParty
	index
		key = key:upper()
		InPartyChecked = InPartyChecked || {}
		if InPartyChecked[key]
			return InPartyChecked[key]>0
		end
		for i=1,6
			if RPGParty[i]:upper()==Key:upper()
				InPartyChecked[key]=1
				return true
			end
		end
		InPartyChecked[key]=0
		return false
	end
end

group MScript

	readonly int SchuifDeur_Links  = 368
	readonly int SchuifDeur_Rechts = 400
	readonly int VaultDoor = 608
	int Schuif = 0
	bool SchuifOpen
	int VaultY = 0
	bool VaultOpen

	Void MapMusic()
		if gv.gbool.WendickaDream
			Music.Play("Music/Scenario/Comfortable Mystery.ogg")
			gv.gbool.WendickaDream = false
			return
		end
		if gv.gstring.Hawk_Music==""
			gv.gstring.Hawk_Music="Music/Hawk/001.ogg"
		end
		Music.Play(gv.gstring.Hawk_Music)
	end

	void Bridge()
		static bool Removals
		Map.HideButLabel("Bridge")
		Map.ShowObj("PLAYER")
		if !Removals
			// Wendicka is no go area... and Foxy and Yirl will always be in the party
			// while in the hawk
			for ch in each { "Crystal", "ExHuRU","Xenobi","Rolf" }				
				switch ch
					case "Crystal"
						if !InParty.Wendicka
							// When Wendicka is unavailable, Crystal takes over!
							Map.Kill("NPC_Hawk_Crystal")
						end
					default
						if !InParty[ch]
							Map.Kill("NPC_Hawk_"..ch)
						end					
				end							
			end
			Removals=true
		end
	end

	void KickOff()
		Map.Leader="Wendicka"
		Player.Wind="North"
		Bridge()
		MapText("ARRIVAL.KICKOFF")
	end
	
	void Talk(string ch)
		Player.WalkTo("SPOT_Hawk_"..ch)
		MapText(ch.."."..__hawk)
	end
	
	void NPC_Hawk_Crystal()
		Talk("Crystal")
		// TODO: Sisters in all but blood
	end
	
	void NPC_Hawk_ExHuRU()
		Talk("ExHuRU")
	end
	
	void NPC_Hawk_Yirl()
		Talk("Yirl")
		// TODO: Follow me
	end
	
	void Hawk_Kick_Reggie(int idx,__Inf)
		Static Table Kicker = {}
		Kicker[idx] = Kicker[idx] || {__inf}
		for k in each(Kicker[idx])
			if __hawk==k
				KickReggie("NPC_Hawk_Foxy","NPC_Hawk_Reggie","South")
			end
		end
	end
	
	void NPC_Hawk_Foxy()
		Talk("Foxy")
		Hawk_Kick_Reggie(1,"NAVULPINA")
		// TODO: Pickpocket
	end
	
	void NPC_Hawk_Reggie()
		Talk("Reggie")
		Hawk_Kick_Reggie(2,"ANHYSBYS.REJOIN","CRYSTALFATHER","WENDICKAGONE")
	end
	
	void NPC_Hawk_Xenobi()
		Talk("Xenobi")
		// TODO: Vitalize
	end
	
	Void NPC_Hawk_Rolf()
		Talk("Rolf")
		// Kolme
		If RPGChar.Crystal.List.ARM.Has("DOPING_SHOT") && RPGChar.Rolf.List.ABL.Has("EXHURU_KOLME") && (!gv.gbool.Wendicka_Kolme)
			gv.gbool.Wendicka_Kolme=true
			MapText("TEACH.ROLF")
			RPGChar.Wendicka.List.LEARN.Add="EXHURU_KOLME"
		End
	End
	

	void Update()
		if InParty.Wendicka 
			if Map.Leader!="Wendicka"
				Map.Leader = "Wendicka"
			end
		elseif Map.Leader != "Crystal"
			Map.Leader = "Crystal"
		end
		Map.SetObj("Schuifdeur_Links" ,"X",SchuifDeur_Links -Schuif)
		Map.SetObj("Schuifdeur_Rechts","X",SchuifDeur_Rechts+Schuif)
		if SchuifOpen && Schuif<30
			Schuif++
		elseif (!SchuifOpen) && Schuif>0
			Schuif--
		end
		Map.SetObj("Vault_Door","Y",VaultDoor+VaultY)
		if VaultOpen && VaultY<62
			VaultY++
		elseif (!VaultOpen) && VaultY>0
			VaultY--
		end
	end
	
	void ShowBack()
		static bool Bakina_Checked
		Map.HideButLabel("Back")
		Map.ShowObj("PLAYER")
		if !Bakina_Checked
			if !gv.gbool.Found_Bakina
				Map.Kill("NPC_Bakina")
			end
			Bakina_Checked=true
		end
	end
	
	void Vault()
		FMenu.Go(Map.Leader,"VAULT")
	end
	
	void Sesam_OpenU()
		SchuifOpen=true
	end
	
	Void Sesam_SluitU()
		SchuifOpen=false
	end
	
	Void OpenVaultDoor()
		VaultOpen=true
	End
	
	Void CloseVaultDoor()
		VaultOpen=false
	end
	
	Void NPC_Terminal()
		State.LoadNew("FLOW_TERMINAL","Script/Flow/FlowTerminal.neil")
		State.GoToFlow("TERMINAL")
	End
	
	Void NPC_Nav()
		if !Done.Done_Tutorial_Hawk_NavigationPanel
			MapText("NAVTUTORIAL")
		end
		// I must be 100% sure the Hawk Flow is loaded. 
		// Seems almost impossible that it isn't, though
		State.LoadNew("FLOW_HAWK","Script/Flow/FlowHawk.Neil")
		State.GoToFlow("HAWK")
	End
	
	Void ARRIVE_PHYSILLIUM()
		//Lua.error("next event not yet present")
		Map.Spawn("ScenarioWendicka","PLAYER")
		Map.HideObj("PLAYER")
		Map.SetObj("ScenarioWendicka","Texture","GFX/Actors/Player/Wendicka.South.jpbf")		
		Map.SetObj("Deken","W",800)
		MapText("ARRIVAL_PHYSILLIUM_A")
		SchuifOpen = true
		Map.ObjWalkTo("ScenarioWendicka","Scotty")
		for i=1,35
			Update()
			Graphics.Cls()
			Map.DrawScreen()
			Graphics.Flip()
			FPS.WaitMinTicks()
			Events.Poll()
		end
		//Player.WalkTo("Scotty")
		MapText("ARRIVAL_PHYSILLIUM_B")
		for i,ch in ipairs { "Crystal", "ExHuRU", "Yirl", "Foxy", "", "" }
			RPGParty[i]=ch
		end
		Map.GotoMap("PHYSILLIUM - THE RUINS OF THE Y ANHYSBYS","#000","Start")
		Schedule.Add("SPAWN.RESET")
	end
	
	
	// Her name is BaNiKa.... But by mistake her name is quite often spelled
	// Bakina in some system references. I was too lazy to fix all that
	Void NPC_Bakina()
		MapText("BAKINA.NEW")
		State.LoadNew("FLOW_BANIKA","Script/Flow/FlowBanika.Neil")
		State.GoToFlow("BANIKA")
		Schedule.Add("Banika_Bye")
	end
	
	void Banika_Bye()
		MapText("BAKINA.BYE")
	end
	
	void NPC_Upgrade()
		if !Done.FirstUpgrade
			MapText("UPGRADEA")
		else
			MapText("UPGRADE")
		end
		FMenu.Go(Map.Leader,"UPGRADE")
	end
	
	Void WendickaSaved()
		Bridge()
		Map.Leader="Wendicka"
		Player.Wind="North"
		MapText("GODDESS")
		Map.GoToMap("Vulpina - Town","TOWN","TownStart")
		Schedule.Add("ArrivalFirstTime")
	end
	
	Void Volcania_Pirates()
		Music.Play("Music/Sys/Silence.ogg")
		MapText("PIRATES_A")
		Map.HideButLabel("LichtUit")
		MapText("PIRATES_B")
		For i=1,20
			String p = sprintf("Yo_Ho_%02d_Yo_Ho_A_Pirates_Life_For_Me",i)
			Map.Spawn(p,sprintf("Pirate%d",i))
			Map.SetObj(p,"TEXTURE","GFX/Actors/SinglePic/Pirates/Pirate.png")
			CSayF("Spawned: %s",p)
		End
		String l = "Today_will_be_known_as_the_day_you_almost_captured_Jacob_Spreeuw"
		Map.Spawn(l,"PirateLeader")
		Map.SetObj(l,"TEXTURE","GFX/Actors/SinglePic/Pirates/PirateLeader.png")
		Map.HideButLabel("Bridge")
		Map.ShowObj("PLAYER")
		MapText("PIRATES_C")
		gv.gBool.AllowBeam = False
		Map.GoToMap("Volcania - Volcanic Plains","#006","START")
	End
	
	Void GoddessGone()
		Map.HideButLabel("Licht_Uit")
		MapText("VONDELING")
		MapMusic()
		Map.HideButLabel("Back")
		Map.ShowObj("PLAYER") 
		Player.Wind="South"
		Map.ShowByLabel("GoddessReleased")
		MapText("RELEASED_A")
		Player.Reloc("GoddessFreeSpot")
		Player.Wind="North"
		Map.HideButLabel("Back")
		Ach.Award("SCENARIO_GODDESSRELEASED")
		Aurina.Rate++
		Hawk.Add("Niz_GatePoint","Nizozemska","Space Port","Nizozemska - Space Port","SPACEPORT")
		__hawk = "GODDESSFREE"
	End

	Void Draw()
	End
	
	void Load()
	end
	
	CONSTRUCTOR
		ZA.Enter("ShowBridge1","Bridge")
		ZA.Enter("ShowBridge2","Bridge")
		ZA.Enter("Schuifdeur_Open","Sesam_OpenU")
		ZA.Leave("Schuifdeur_Open","Sesam_SluitU")
		ZA.Enter("ShowBack","ShowBack")
		ZA.TabEnter({"Vault","OpenVaultDoor"})
		ZA.Leave("OpenVaultDoor","CloseVaultDoor")
		Party.Recover()
		gv.gbool.WendickaSwitchBlocked = false
		gv.gBool.AllowBeam=true
		Map.AllowQuitSave=false
	end

end