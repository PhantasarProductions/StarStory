// License Information:
// **********************************************
// 
// src/Tricky Script/Script/MapScript/Phantasar - Frendor.neil
// (c) Jeroen Broks, 2021, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 21.05.08
// End License Information

#use "Script/Use/Anyway"
#use "Script/Use/Map/KickReggie"
#use "Script/Use/Map/WorldMap"

#MKL_Version "Star Story - Phantasar - Frendor.neil" "21.05.08"
#MKL_Lic     "Star Story - Phantasar - Frendor.neil" "Phantasar Closed License"

#macro TaalGeleerd gv.gbool.LearnedToSpeakTheLanguageOfPhantasar

group MScript

#region Alt save spot
	Void NPC_SaveSpot()
		Party.Recover()
		Spawn.Reset()
		MSave.ToSave()
	End
#endregion

#region NPC
	Void QTaal(string niet,string wel)
		if TaalGeleerd
			MapText(wel)
		else
			MapText(niet)
		end
	End
	
	Void QQTaal(string Pers)
		QTaal(Pers..".CANNOTSPEAK",Pers..".CANSPEAK")
	End

	Void NPC_Feenalaria()
		if !TaalGeleerd
			MapText("FEENA_HUH")
			return
		end
		if (!Done.Slagimon_Unlocked)
			MapText("FEENA_1A")
			PPop.Pop("Feena","North")
			Map.ShowObj("Feena_Reggie")
			MapText("FEENA_1B")
			KickReggie("PartyPopped_Foxy","Feena_Reggie","East")
			MapText("FEENA_1C")
			PPop.UnPop()
			Party.GiveEXP(1234)			
			return
		end
		if !gv.gBool.Completed_GhostHouse
			MapText("FEENA_ONLYHOPE")
			return
		end
		Lua.error("Feenalaria not yet scripted for this particular situation")
	end
	
	Void NPC_Irravonia()
		QQTaal("IRRAVONIA")
	End
	
	Void NPC_Scyndi()
		QQTaal("SCYNDI")
	End
	
	Void NPC_Rachel()
		If !TaalGeleerd
			MapText("RACHEL.CANNOTSPEAK")
			return
		End
		If RPGChar.Crystal.List.ARM.Has("ARKSMASH") || (Map.Leader!="Crystal")
			MapText("RACHEL.NOTCRYSTAL")
			Return
		End
		if gv.gBool.Completed_Airom
			Lua.error("Rachel not yet fully scripted")
			Return
		End
		MapText("RACHEL.CRYSTAL")
		gv.gbool.Airom_Unlocked = True
		//Lua.error("Rachel not yet fully scripted")
	End
	
	Void NPC_Merya()
		QQTaal("MERYA")
		if !TaalGeleerd
			return
		end
		if !Done.Merya_StouteJongens
			int deg=0
			number scale=0
			var RPic = Image.Load("GFX/Scenario/Reggie_Big.png")
			Var RinkelDeKinkel = Audio.Load("Audio/SFX/scenario/Reggie_Crash.ogg")
			RPic.HotCenter()
			Map.Leader="Foxy"
			MapText("MERYA.REGGIE")
			Player.Wind="South"
			Repeat
				deg++
				scale+=0.05
				Graphics.Cls()
				Map.DrawScreen()
				Graphics.Scale(scale,scale)
				__white
				Image.Rotate=deg
				RPic.XDraw(Screen.W div 2,Screen.H div 2)
				Graphics.Scale(1,1)
				Graphics.Flip()
				FPS.WaitMinTicks()
			LoopWhile RPic.Height*scale<=Screen.H*2
			RinkelDeKinkel.Play()
			Music.Play("Music/Sys/Silence.ogg")
			RPic.Kill()
			RPic=nil
			Map.SetObj("ColorBars","X",Map.CamX- 1)
			Map.SetObj("ColorBars","Y",Map.CamY- 1)
			Map.SetObj("ColorBars","W",Screen.W+ 2)
			Map.SetObj("ColorBars","H",Screen.H-95)
			for c in each{"R","G","B"}
				Map.SetObj("ColorBars",c,255)
			end
			MapText("FOXY.OOPS")
			Map.Kill("ColorBars")
			Ach.Award("SCENARIO_CRASH")
			Party.GiveEXP(20000)
			Music.Play("MUSIC/TOWN/ENCHANTED VALLEY.OGG")
		end
		//Lua.error("Merya's further script not yet done!")
	End
	
	Void NPC_Merchant()
		QQTaal("MERCHANT")
		if TaalGeleerd
			//Lua.error("Merchant not yet scripted!")
			FMenu.Go(Map.Leader,"PHANSHOP")
		end
	End
	
	Void NPC_Rosetta()
		If TaalGeleerd
			MapText("ROSETTA")
		Else
			MapText("ROSETTA_PRE")
			PPop.Pop("Rosetta","North")
			MapText("ROSETTA_FIRST")
			TaalGeleerd = true
			Party.GiveEXP(1000)
			Aurina.Rate++
			PPop.UnPop()
		End
	End
	
#endregion

#region Other scripts

	Bool DoesCrystalHaveAllARMS()
		Table ARMS = {}
		ARMS += "DART"
		ARMS += "HEALINGSPRAY"
		ARMS += "WATERGUN"
		ARMS += "POISONDART"
		ARMS += "VIRUSBOMB"
		ARMS += "MUNCHHAUSEN"
		ARMS += "MULTIBLAST"
		ARMS += "FLAMETHROWER"
		ARMS += "MINICANNON"
		ARMS += "ICEBULLET"
		ARMS += "STUN_GUN"
		ARMS += "DOPING_SHOT"
		ARMS += "BIOHAZARD"
		ARMS += "HEALINGSHOWER"
		ARMS += "ROCKTHROWER"
		ARMS += "NAPALMSHOWER"
		ARMS += "RISINGNOVA"
		bool ret = true                      
		bool have
		for ARM in each(ARMS) 
 			//have = RPGChar.ListHas('Crystal','ARMS',ARM)==1
 			have = RPGChar.Crystal.List.ARM.Has(ARM)
 			ret = ret && have
 			CSay("Check ARM: "..ARM.."  => "..tostring(have).." == ret> "..tostring(ret))
 		end
 		return ret
 	end
 	
 	Void StartZone() 		
 		if !DoesCrystalHaveAllARMS()
 			Map.Kill("NPC_Rachel")
 			CSay("As Crystal does not yet have all ARMS, Rachel should not be present on the map")
 		end
 		if !gv.gBool.Completed_GhostHouse
 			Map.Kill("NPC_MT_MARRILONA")
 			CSay("Marrilona's not here yet. Finish the ghost house first!")
 		end
 		Map.Kill("StartZone")
 		Map.HideObj("Feena_Reggie")
 	End
 	
 	void ByeBye()
 		WorldMap("Phantasar")
 	end
	
#endregion


#region Just the required callbacks that had to be there
	Void Update()
	End

	Void Draw()
	End
	
	Void Load()
	End
#endregion
	
	CONSTRUCTOR
		ZA.TabEnter{"StartZone","Byebye"}
		Map.AllowQuitSave=false
	End

end