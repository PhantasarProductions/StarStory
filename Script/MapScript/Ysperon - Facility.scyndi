// License:
// **********************************************
// 
// src/Script/Script/MapScript/Ysperon - Facility.scyndi
// (c) Jeroen Broks, 2021, 2025, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// 
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// 
// version: 25.01.30 XIV
// 
// End License


Script

#use "Libs/Music"

#use "Libs/SCI/Sys"
#use "Libs/SCI/Events"
#use "Libs/SCI/Kthura"
#use "Libs/SCI/Graphics"
#use "Libs/SCI/InterVar"

#use "Script/Use/Link"
#use "Script/Use/Anyway"
#use "Script/Use/Convert/RPG2Stat"
#use "Script/Use/Map/KickReggieUse"


//#MKL_Version "Star Story - Ysperon - Facility.neil" "21.05.31"
//#MKL_Lic     "Star Story - Ysperon - Facility.neil" "Phantasar Closed License"

#undef DEBUG_SCHUIFDEUR

#macro StartGuardsKilled gbool.EnteredTheEugorvniaScienceFacilityToSetTheFoolishRescueMissionInMotion_ButLetsKillTheGuardsAtTheEntranceFirst
#macro WendickaFreed gbool.WendickaFreedFacility

class SchuifDeur
	static table Mapje = {}
	
	readonly int prevLinks
	readonly int prevRechts
	readonly int nextLinks
	readonly int nextRechts
	static bool PrevOpen
	static bool NextOpen
	int NextSchuif
	int PrevSchuif
	
	Constructor
		for d in each{[0]="PrevLinks","PrevRechts","NextLinks","NextRechts"}
			if Map.HasTag(d)
				self[d]=Map.ObjInt(d,"X")
				CSayF("Layer %s: Schuifdeur: %s => x = %04d",Map.Layer,d,self[d])
			else
				self[d]=-1
				CSayF("Layer %s: Schuifdeur: %s => Non-Existent!",Map.Layer,d)
			end
		end
	end
	
	static void Check(string OL,string CL)
		Mapje[CL] = Mapje[CL] || new SchuifDeur()
		Mapje[CL].CheckFl(OL,CL)
	end
	
	void CheckFL(string OL, string CL)
		if PrevOpen 
			if OL!=CL
				PrevSchuif=30
			elseif PrevSchuif<30
				PrevSchuif++
			end
		else
			if OL!=CL
				PrevSchuif=0
			elseif PrevSchuif>0
				PrevSchuif--
			end
		end
		if PrevLinks>=0
			Map.SetObj("PrevLinks","X",PrevLinks-PrevSchuif)
			Map.SetObj("PrevRechts","X",PrevRechts+PrevSchuif)
		end
		if NextOpen 
			if OL!=CL
				NextSchuif=30
			elseif NextSchuif<30
				NextSchuif++
			end
		else
			if OL!=CL
				NextSchuif=0
			elseif NextSchuif>0
				NextSchuif--
			end
		end
		if NextLinks>=0
			Map.SetObj("NextLinks","X",NextLinks-NextSchuif)
			Map.SetObj("NextRechts","X",NextRechts+NextSchuif)
		end
	end
	
	void Debug()
		LocExtern DFnt "DebugFontSchuifdeurDebug"
#if DEBUG_SCHUIFDEUR
		//static var DFnt = ImageFont.Obtain("BOXTEXT")
		DFnt = DNft || ObtainFont("BOXTEXT")
		dFnt.DarkText(sprintf("Prev Open: %s",lua.tostring(PrevOpen)),5,0)
		dFnt.DarkText(sprintf("Prev Corr: %04d",PrevSchuif),5,30)
		dFnt.DarkText(sprintf("Next Open: %s",lua.tostring(NextOpen)),5,60)
		dFnt.DarkText(sprintf("Next Corr: %04d",NextSchuif),5,90)
#fi
	end		
end
		

group MScript

	bool BossOpen = false
	int BossX
	
	void BossDoor()
		if BossOpen && BossX<30
			BossX++
		elseif (!BossOpen) && BossX>0
			BossX--
		end
		If Map.Layer=="#006"
			Map.SetObj("BossRechts","X",416+BossX)
			Map.SetObj("BossLinks","X",384-BossX)
		End
	End
	Void OpenBoss()
		BossOpen=true
	end
	Void Leave_OpenBoss()
		BossOpen=False
	end

	void Boss()
		int lv = math.min(10000,(100*__skill)*Caps.Cycle)
		StartCombat.Foe("Boss/AstrilopupElite",lv)
	end
	
	int GeheimX
	bool GeheimOpen
	void OpenGeheim()
		GeheimOpen=true
	end
	void Leave_OpenGeheim()
		GeheimOpen=false
	end
	void Geheim()
		if Map.Layer!="#012"
			return
		end
		if GeheimOpen && GeheimX<29
			GeheimX++
		elseif (!GeheimOpen) && GeheimX>0
			GeheimX--
		end
		Map.SetObj("GeheimLinks" ,"X", 96-GeheimX)
		Map.SetObj("GeheimRechts","X",128+GeheimX)
	end
	void HideGeheim()
		Map.HideByLabel("Geheim")
	end
	void ShowGeheim()
		Map.ShowByLabel("Geheim")
		//Map.ShowObj("PLAYER")
	end
	
	
	void GroteKamer()
		Map.HideByLabel("Bonus")
	end
	void OpenBonus()
		Map.ShowByLabel("Bonus")
	end

	void MapMusic()
		if !Done.DoNotStartTheMusicForTheFacilityYet
			Music.Play("Music/Sys/Silence.ogg")
		else
			Music.Play("Music/Dungeon/AstrilopupBase.ogg")
		end
	end

	void Wendicka_Intro()
		Map.HideButLabel("Fake")
		MapText("WENDICKA_A")
		Map.ShowButLabel("Fake")
		Map.HideObj("PLAYER")
		MapText("WENDICKA_B")
		Map.Spawn("Actor_McLeen","AstriloWest")
		Map.SetObj("Actor_McLeen","Texture","GFX/Actors/McLeen/Masked.South.jpbf")
		Map.ObjWalkTo("Actor_McLeen","Wendicka_McLeen")
		for i=1,132
			int x=math.min(31,i)
			if i>100 
				x=132-i
			end
			Map.SetObj("NextLinks","X",320-x)
			Map.SetObj("NextRechts","X",352+x)
			Graphics.Cls()
			Map.DrawScreen()
			Graphics.Flip()
			//FPS.WaitMinTicks()
			Events.Poll()
		end
		MapText("WENDICKA_C")
		Map.Kill("Actor_McLeen") // I must not forget to put him back 
		Map.GoToLayer("#001","Start")
	end
	
	Void StartDungeon()
		if ! Done.EnteredTheEugorvniaScienceFacilityToSetTheFoolishRescueMissionInMotion
			Spawn.Reset()
			gBool.AllowBeam=False
		end
		if !StartGuardsKilled
			MScript.MapMusic()
			PPop.Pop("PopStart","North")
			MapText("ENTER_A")					
			StartCombat.Prepare()
			StartCombat.Foe("Reg/Cyborg Gunner",math.random(Map.Level div 2,Map.Level))
			StartCombat.Foe("Reg/Cyborg Captain",math.random(Map.Level div 2,Map.Level))
			StartCombat.Foe("Reg/Astrilopup",math.random(Map.Level div 2,Map.Level))
			StartCombat.Foe("Reg/Astrilopup",math.random(Map.Level div 2,Map.Level))
			//StartCombat.Tune = StartCombat.RandomTune
			StartCombat.Tune = "*NOCHANGE*"
			StartCombat.Boss = false
			StartCombat.Arena = "Facility.png"	
			StartCombat.Begin()
			Schedule.Add("StartDungeon_II")
			Party.GiveEXP(20)
		end
	end
	Void StartDungeon_II()
		For a in each{[0]="AstrilGuard_A","AstrilGuard_B","Cyb_A","Cyb_B"}
			CSayF("Removed %s permanently!",a)
			Map.Kill(a,true)
		End
		Party.GiveEXP(2400 div __skill)
		StartGuardsKilled=true
		MapText("ENTER_B")
		PPop.UnPop()
	End

	table BeenFloor
	get bool BeenOnThisFloor
		return BeenFloor[Map.Layer]
	end
	
	void FloorExp()
		if !BeenOnThisFloor
			CSay("First time on floor "..Map.Layer.."! Floor Experience bonus!")
			Switch __skill
				case 1
					Party.GiveEXP(3000)
				case 2
					Party.GiveEXP((Lua.tonumber(Map.Layer)||1)*100)
				case 3
					Party.GiveEXP(1)
				default
					Lua.error("Skill error")
			end
		end
	end
	
	void SchuifPrev()
		CSayF("PREV: Sesam open u")
		SchuifDeur.PrevOpen = true
	end
	void Leave_SchuifPrev()
		CSayF("PREV: En ga maar weer dicht")
		SchuifDeur.PrevOpen = false
	end
	void SchuifNext()
		CSayF("NEXT: Sesam open u")
		SchuifDeur.NextOpen = true
	end
	void Leave_SchuifNext()
		CSayF("NEXT: En ga maar weer dicht")
		SchuifDeur.NextOpen = false
	end
	
	void To999()
		Map.GoToLayer("#999","Start")
		if !WendickaFreed
			Schedule.Add("FREEWENDICKA_Fight")
		end
	end
	
	void FreeWendicka_Fight()
		Map.Spawn("Actor_McLeen","Wendicka_McLeen")
		PPop.Pop("Wen","West")
		Map.SetObj("Actor_McLeen","Texture","GFX/Actors/McLeen/Masked.South.jpbf")
		for c in each{'AB'}
			Map.SetObj("Astrilo_"..chr(c),"Texture","GFX/Actors/SinglePic/Astrilopup/Astrilopup_E.png")
		end
		MapText("ASTRILOPUP_ALARM")
		
		StartCombat.Prepare()
		StartCombat.Foe("Reg/Astrilopup",math.random(Map.Level div 2,Map.Level))
		StartCombat.Foe("Reg/AstrilopupGuard",math.max(1,math.random(Map.Level div 5,Map.Level div 2)))
		StartCombat.Tune = "*NOCHANGE*"
		StartCombat.Boss = false
		StartCombat.Arena = "Facility.png"	
		StartCombat.Begin()
		
		
		Schedule.Add("FREEWENDICKA")
		//Lua.error("En de rest komt later :-P")
	end
	
	void FreeWendicka()
		for c in each{'AB'}
			Map.Kill("Astrilo_"..chr(c),true)			
		end
		Map.Spawn("__Reggie","Wen_Reggie")
		Map.SetObj("__Reggie","Texture","GFX/Actors/Reggie/ReggieW.png")
		WendickaFreed = true
		MapText("FREE_WENDICKA_A")
		Beam.Quick("Actor_McLeen",true,true)
		MapText("FREE_WENDICKA_B")
		Graphics.Cls()
		Graphics.Flip()
		Events.Poll()
		Sys.Sleep(1200)
		Map.SetObj("WendickaInChains","Texture","GFX/Textures/Wendicka/Naked/WendickaNakedStandEast.png")
		MapText("FREE_WENDICKA_C")
		KickReggie("PartyPopped_Foxy","__Reggie","South")
		MapText("FREE_WENDICKA_D")
		Music.Play("Music/Scenario/Panic Stations.ogg")
		SchuifDeur.PrevOpen = true
		SchuifDeur.NextOpen = true
		for i=1,5
			for wnd in each{"East","West"}
				var mytag = sprintf("SecurityGuardAstrilopup_%d_%s",i,wnd)
				string tow = "E"
				if wnd=="East"
					tow="W"
				end
				Map.Spawn(mytag,"Astrilo"..wnd)
				Map.SetObj(mytag,"Texture","GFX/Actors/SinglePic/Astrilopup/Astrilopup_"..tow..".png")
				Map.ObjWalkto(mytag,sprintf("A%s%d",wnd,i))
			end
			for j=1,16
				Graphics.Cls()
				Map.DrawScreen()
				self.Update()
				Graphics.Flip()
				//FPS.WaitMinTicks()
				Events.Poll()
			end
		end
		MapText("FREE_WENDICKA_E")
		Map.Kill("__Reggie")
		Map.Kill("WendickaInChains",true)
		For ch in each{"Crystal","Yirl","Foxy","Xenobi"}
			Map.ObjWalkto("PartyPopped_"..ch,"ByeBye")
		end
		Map.ActTrans("Lab")
		Map.SkipFrames(200)
		For i=1,5
			if RPGParty[i]=="ExHuRU"
				RPGParty[i]="Wendicka"
			end
		end
		Map.GoToMap("Hawk","BRIDGE","Welcome")
		Map.Schedule("WendickaSaved")
		RPGChar.Wendicka.Stats.Level = math.min(RPGChar.Yirl.Stats.Level+(3-__skill),10000)
		Party.GiveEXP(1000000)
		gBool.AllowBeam=True
		Hawk.AllowYsperon=False
		//Party.GrabLevel("Wendicka",RPGChar.Yirl.Stats.Level)
	end
	
	
	void Update()
		LocExtern OldLayer "EugorvniaScienceFacility_OldLayer_Update_MScript"
		OldLayer = OldLayer || "Okay, laten we starten!"
		string Layer = Map.Layer
		if Layer != OldLayer
			MScript.FloorExp()
		end
		SchuifDeur.Check(OldLayer,Layer)
		If Layer=="#006"
			MScript.BossDoor()
		End
		MScript.Geheim()
		OldLayer = Layer
	end
	
	void ToFacility012()
		Map.GoToLayer("#012","Einde")
	end
	
	void ToFacility2()
		Map.GoToMap("Ysperon - Facility 2","#013","Start")
	end
	
	Void GoBackToTheLab()
		Map.GoToMap("Ysperon - Facility","#999","Einde")
	End
	
	Void Boss_ExHuRU()
		Kthura.KillAutoVisible()
		PPop.Pop("Ex","North")
		MapText("EXHURU1")
		Map.Spawn("BSEX","SpotExHuRU")
		Map.SetObj("BSEX","Texture","GFX/Actors/Player/ExHuRU.South.jpbf")
		Map.ObjWalkTo("BSEX","MeetExHuRU")
		int L = Map.ObjInt("ExLinks","X")
		int R = Map.ObjInt("ExRechts","X")
		for O=1,33 
			Graphics.Cls()
			Map.SetObj("ExLinks","X",L-O)
			Map.SetObj("ExRechts","X",R+O)
			Map.DrawScreen()
			Graphics.Flip()
			//FPS.WaitMinTicks()
			CSayF("ExHuRU (%04d,%04d): ",Map.ObjInt("BSEX","X"),Map.ObjInt("BSEX","Y"))
		End
		MapText("EXHURU2")
		Schedule.Add("Terminated")
		Kthura.SetAutoVisible()
		StartCombat.Prepare()
		StartCombat.Foe("Boss/ExHuRU",RPGChar.Rolf.Stats.Level)
		StartCombat.Tune = "Music/Combat/SpecialBoss/ExHuRU.ogg"
		StartCombat.Boss = True
		StartCombat.Arena = "Facility.png"	
		StartCombat.Begin()				
	End
	
	Void Terminated()
		Map.SetObj("BSEX","Texture","GFX/Combat/Fighters/Hero/ExHuRu.Dead.png")		
		Map.ObjWalkTo("PartyPopped_Wendicka","Wendicka_Analyse")
		MapText("EXHURU3")
		Party.GiveEXP(3500 div __skill)
		PPop.UnPop()
		Map.Kill("Boss_ExHuRU",true)
		Map.Kill("ExLinks",true)
		Map.Kill("ExRechts",true)
	End
	
	Void Event_McLeen()
		if Done.Mol_Definitief_Ontmaskerd
			return
		end
		PPop.Pop("END","NORTH")
		MapText("DEADEND1")
		SchuifDeur.NextOpen = true
		for j=1,32
			Graphics.Cls()
			Map.DrawScreen()
			self.Update()
			Graphics.Flip()
			//FPS.WaitMinTicks()
			Events.Poll()
		end
		MapText("DEADEND2")
		Map.GoToLayer("MOL","Pos_Wendicka")
		Map.HideObj("PLAYER")
		MapText("MOL1")
		KickReggie("Foxy","Reggie","East")
		MapText("MOL2")
		Party.GiveEXP(1234567)
		__hawk="CRYSTALFATHER"
		Hawk.Add("Start","Vulpina","Flower Forest - South Area","Vulpina - Flower Forest","#001")
		Ach.Award("SCENARIO_WENDICKAPREGNANT")
		Map.GoToMap("Hawk","BRIDGE","Scotty")
	End

	void Draw()
#if DEBUG_SCHUIFDEUR
		if SchuifDeur.Mapje[Map.Layer]
			SchuifDeur.Mapje[Map.Layer].Debug()
		end
#fi
	end
	
	void Load()
	end
	
	Void CONS()
		//bool BeenFloorIdx(s,string key)
		ddef BeenFloorIdx
			plua s
			string key
			s,key = infinity
			return Done["Facility_Been_On_Floor_"..right(key,3)]
		end
		BeenFloor = Lua.setmetatable({},{["__index"]=BeenFloorIdx})
		ZA.TabEnter{"StartDungeon","SchuifPrev","SchuifNext","OpenBoss","GroteKamer","OpenBonus","HideGeheim","ShowGeheim","OpenGeheim","To999","ToFacility012","ToFacility2","GoBackToTheLab","Boss_ExHuRU","Event_McLeen"}
		ZA.TabLeave{"SchuifPrev","SchuifNext","OpenBoss","OpenGeheim"}
	end

end

Init
	MScript.CONS()
End
