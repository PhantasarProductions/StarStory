// License Information:
// **********************************************
// 
// src/Tricky Script/Script/MapScript/LostPlanet - Dungeon.neil
// (c) Jeroen Broks, 2021, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 21.03.01
// End License Information

#use "Script/Use/Anyway"

#MKL_Version "Star Story - LostPlanet - Dungeon.neil" "21.03.01"
#MKL_Lic     "Star Story - LostPlanet - Dungeon.neil" "Phantasar Closed License"

#macro ClockDone gv.gbool.Done_Dungeon_ClockPuzzle
#macro IvoDone gv.gbool.Done_Dungeon_IVOPuzzle


// The clock puzzle and Ivo's puzzle, require me a slight different approach.
// This should work, though :P
group TMScript

	int CoolDown = 0

	void Niks()
		// Does nothing at all.
		// "Niks" means "Nothing" in Dutch.
		// I merely required a return value in my QuickMeta section below
		// Or the MapScript linkthrough would fail.
	end

	void Clock(string Tag)
		static table Check
		if ClockDone			
			return
		end
		if Tag=="*RESET*"
			CSay("Resetting the clock puzzle")
			string ol = Map.Layer
			Map.Layer="#003"
			Check = {}
			for i=1,12
				MAP.SetObj("NPC_Clock"..i,"Frame",0)
			end
			Map.Layer=ol
			return
		end
		MAP.SetObj(Tag,"Frame",1)
		for c in each(Check)
			if c==Tag
				CSayF("Clock: Plate '%s' was already set. Total pressed %d. Returning",Tag,#check)
				return
			end
		end
		check += Tag
		CSayF("Clock: Setting plate '%s' - Total pressed %d",Tag,#check)
		if #check>=12
			bool cool = true
			for i=1,12
				cool = cool && check[i]=="NPC_Clock"..i
			end
			if cool
				Map.Kill("ClockDoor",true)
				ClockDone=true
				Party.GiveExp(1200)
				for i=1,12
					Map.Kill("NPC_Clock"..i,true)
				end
			else
				self.Clock("*RESET*")
			end
		end
	end
	
	void ResetClock()
		static bool D
		if !D
			Clock("*RESET*")
			D=true
		end
	end
	
	void Ivo(string Tag)
		static table IvoTable={}
		static table Check
		if IvoDone			
			return
		end
		if Tag=="*RESET*"
			CSay("Resetting Ivo's puzzle")
			//string ol = Map.Layer
			//Map.Layer="#003"
			Check = {}
			for i=1,12
				MAP.SetObj("NPC_Ivo"..i,"Frame",0)
			end
			//Map.Layer=ol
			return
		end
		MAP.SetObj(Tag,"Frame",1)
		for c in each(Check)
			if c==Tag
				CSayF("Clock: Plate '%s' was already set. Total pressed %d. Returning",Tag,#check)
				return
			end
		end
		check += Tag
		CSayF("Ivo: Setting plate '%s' - Total pressed %d",Tag,#check)
		bool cool = true
		for i=1,3
			cool = cool && check && #check>=3 && check[i] && (suffixed(check[i],"4") || suffixed(check[i],"6") || suffixed(check[i],"10")) && check[1]!=check[2] && check[1]!=check[3] && check[2]!=check[3]
		end
		if cool
			Map.Kill("IvoDoor",true)
			IvoDone=true
			Party.GiveExp(2400)
			for i=1,12
				Map.Kill("NPC_Ivo"..i,true)
			end
		elseif #check>=3
			self.Ivo("*RESET*")
		end		
	end
	
	void Boss()
		Int BossLevel
		switch Caps.Cycle
			case 0 1
				BossLevel = 25 + (__skill*5)
			case 2 
				BossLevel = 200 + (__skill*10)
			case 3
				BossLevel = 440 + (__skill*25)
			case 4
				BossLevel = 1234 + (__skill*50)
			case 5 6
				Bosslevel = 5000 + (__skill*100)
			case 7 8
				BossLevel = 5500 + (__skill*250)
			case 9
				BossLevel = 6500 + (__skill*500)
			case 10
				BossLevel = 7000
			case 11
				BossLevel = 8000
			case 12
				BossLevel = 9000
			case 13
				BossLevel = 9500
			case 14
				BossLevel = 9800
			default
				BossLevel = 10000
		end
		int SubjectLevel = math.floor(math.max(5,BossLevel * (0.25*__skill)))
		if Caps.Cycle>16
			SubjectLevel = 10000
		end
		switch __skill
			case 1
				StartCombat.Foe("Boss/FlirMouse_Subject",SubjectLevel)
				StartCombat.Foe("Boss/FlirMouse_King",BossLevel)
				StartCombat.Foe("Boss/FlirMouse_Subject",SubjectLevel)
			case 2
				StartCombat.Foe("Boss/FlirMouse_Subject",SubjectLevel)
				StartCombat.Foe("Boss/FlirMouse_King",BossLevel)
				StartCombat.Foe("Boss/FlirMouse_Subject",SubjectLevel)
				StartCombat.Foe("Boss/FlirMouse_Subject",SubjectLevel)
				StartCombat.Foe("Boss/FlirMouse_Subject",SubjectLevel)
				StartCombat.Foe("Boss/FlirMouse_Subject",SubjectLevel)
			case 3
				StartCombat.Foe("Boss/FlirMouse_Subject",SubjectLevel)
				StartCombat.Foe("Boss/FlirMouse_Subject",SubjectLevel)
				StartCombat.Foe("Boss/FlirMouse_Subject",SubjectLevel)
				StartCombat.Foe("Boss/FlirMouse_Subject",SubjectLevel)
				StartCombat.Foe("Boss/FlirMouse_King",BossLevel)
				StartCombat.Foe("Boss/FlirMouse_Subject",SubjectLevel)
				StartCombat.Foe("Boss/FlirMouse_Subject",SubjectLevel)
				StartCombat.Foe("Boss/FlirMouse_Subject",SubjectLevel)
				StartCombat.Foe("Boss/FlirMouse_Subject",SubjectLevel)
		end
		StartCombat.StartScript = "FlirMouse_King"
		Schedule.Add("ShowPostBoss")
	end
	
	void ShowPostBoss()
		Map.ShowByLabel("PostBoss")
	end
	
	void EnterBoss()
		static bool EnteredBefore = false
		if !EnteredBefore
			Map.HideByLabel("PostBoss")
			EnteredBefore = true
		end
	end
	
	Void NPC_FreeAtLast()
		Map.GoToMap("LostPlanet - Junkyard","#001","Start")
		if !Done.Done_Completed_Dungeon_Lost_Planet
			Party.GiveEXP(1000)
			Schedule.Add("Welcome")
		end
	end
	

	void Update()
		if CoolDown>0
			CoolDown--
		end
	end

	void Draw()
	end
	
	void Load()
	end
	
	CONSTRUCTOR
		//Clock("*RESET*")
		//Ivo("*IVO*")
		for z in each{"ResetClock","EnterBoss"}
			ZA.Enter(z,z)
		end
	end

end


// Fake MapScript
// Intercept Ivo's puzzle and the clock puzzle.
// The rest must just be linked through normally.
quickmeta MScript
	index
		if prefixed(key,"NPC_Clock")
			if TMScript.CoolDown>0
				CSayF("Clock: CoolDown on %d - Request ignored!",TMScript.CoolDown)
				return TMScript.Niks
			end
			TMScript.Clock(key)
			TMScript.CoolDown = 40
			return TMScript.Niks
		elseif prefixed(Key,"NPC_Ivo")
			if TMScript.CoolDown>0
				CSayF("Clock: CoolDown on %d - Request ignored!",TMScript.CoolDown)
				return TMScript.Niks
			end
			TMScript.Ivo(key)
			TMScript.CoolDown = 40
			return TMScript.Niks
		else
			return TMScript[key]
		end
	end
end