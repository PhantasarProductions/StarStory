// License:
// **********************************************
// 
// src/Script/Script/MapScript/LostPlanet - Bonus_Dark_Caves_of_Shilington.scyndi
// (c) Jeroen Broks, 2021, 2025, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// 
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// 
// version: 25.01.25 XIV
// 
// End License

Script

#use "Libs/SCI/Sys"
#use "Libs/SCI/Events"
#use "Libs/SCI/Graphics"
#use "Libs/SCI/InterVar"

#use "Script/Use/Link"
#use "Script/Use/Anyway"

#use "Script/Use/Convert/RPG2Stat"

//#MKL_Version "Star Story - LostPlanet - Bonus_Dark_Caves_of_Shilington.neil" "21.10.09"
//#MKL_Lic     "Star Story - LostPlanet - Bonus_Dark_Caves_of_Shilington.neil" "Phantasar Closed License"

void Niks()
end

group MScript

	readonly int Secrets = 5

	void Update()
	end

	void Draw()
	end
	
	void Load()
	end
	
	void Dom40()
		Map.SetObj("PLAYER","Dominance",40)
		CSay("Player's dominance set to 40")
	end
	
	void HideAllSecrets()
		for i=1,Secrets
			Map.HideByLabel("Secret"..i)
		end
	end
	
	table Show
	table Hide
	
	void Boss()
		switch __skill
			case 1
				StartCombat.Foe("Boss/SuperSyss",math.ceil(RPGChar.Wendicka.Stat.Level*0.75))
			case 2
				StartCombat.Foe("Reg/Syss",Math.random(2,RPGChar.Wendicka.Stat.Level))
				StartCombat.Foe("Boss/SuperSyss",math.ceil(RPGChar.Wendicka.Stat.Level*1.25))
				StartCombat.Foe("Reg/Syss",Math.random(2,RPGChar.Wendicka.Stat.Level))
			case 3
				StartCombat.Foe("Reg/Syss",Math.random(20,math.max(20,RPGChar.Wendicka.Stat.Level)))
				StartCombat.Foe("Boss/Syss",math.ceil(RPGChar.Wendicka.Stats.Level*1.25))
				for i=1,4
					StartCombat.Foe("Reg/Syss",Math.random(20,math.max(20,RPGChar.Wendicka.Stat.Level)))
				end
			default
				Lua.error("Unknown skill!")
		end
	end
	
	
	void FirstJump()
		Player.Wind="South"
		for i=1920,2080
			Graphics.Cls()
			Player.Y=i
			Map.DrawScreen()
			Graphics.Flip()
			Events.Poll()
			Sys.WaitMinTicks()
		end
	end
	
	void JumpDown()
		Player.Wind="South"
		for i=3360,3488
			Graphics.Cls()
			Player.Y=i
			Map.DrawScreen()
			Graphics.Flip()
			Events.Poll()
			Sys.WaitMinTicks()
		end
		Ach.Award("SECRETDUNGEON_DARKCAVES")
		if !Done.Completed_DarkCavesShillington
			Party.GiveEXP(2500)
		end
	end
	
	void NPC_EMERALD()
		if item.give("EQP_EMERALD",Map.Leader)
			Schedule.Add("MSG: "..Map.Leader.." obtained an Emerald")
			bool perm
			switch __skill
				case 1
					perm = math.random(1,20)==1
				case 2
					gint.DarkCaveEmerald++
					perm = math.random(1,math.max(1,20-gint.DarkCaveEmerald))==1
				case 3
					perm = true
				default
					Lua.error("What?")
			end
			CSayF("Removing Emerald.... Permantent: %s",Lua.tostring(perm))
			Map.Kill("NPC_EMERALD",perm)
		end
	end
	
	void NPC_RUBY()
		if item.give("EQP_RUBY",Map.Leader)
			Schedule.Add("MSG: "..Map.Leader.." obtained a Ruby")
			bool perm
			switch __skill
				case 1
					perm = math.random(1,20)==1
				case 2
					gint.DarkCaveRuby++
					perm = math.random(1,math.max(1,20-gint.DarkCaveRuby))==1
				case 3
					perm = true
				default
					Lua.error("What?")
			end
			CSayF("Removing Ruby.... Permantent: %s",Lua.tostring(perm))
			Map.Kill("NPC_RUBY",perm)
		end
	end
	
	Void Leave()
		Map.Load("LostPlanet - GrassJungle_2")
		Schedule.Add("SPAWN:FromSecret")
	end
	
	
	void _CONSTRUCTOR()
		for i=1,Secrets
			string T = "Secret"..i
			ZA.Enter(T,"Show."..T)
			if i!=4
				ZA.Leave(T,"Hide."..T)
			end
		end
		ZA.Enter("ByeSecret4","Hide.Secret4")
		//var Sh(s,string k)
		ddef Sh
			plua s
			string k
			s,k=Infinity
			Map.ShowByLabel(k)
			return Niks
		end
		//var Hd(s,string k)
		ddef Hd
			plua s
			string k
			s,k=Infinity			
			Map.HideByLabel(k)
			return Niks
		end
		Show = Lua.setmetatable({},{["__index"]=Sh,["__newindex"]=Niks})
		Hide = Lua.setmetatable({},{["__index"]=Hd,["__newindex"]=Niks})
		Schedule.Add("HideAllSecrets")
		for l in each {'abc'}
			ZA.Enter("Dom40"..Chr(l),"Dom40")
		end
		for j in each {"FirstJump","JumpDown","Leave"}
			ZA.Enter(j,j)
		end
	end
end

Init
	MScript._CONSTRUCTOR()
end
