// License Information:
// **********************************************
// 
// src/Tricky Script/Script/MapScript/Prologue_Yaqirpa.neil
// (c) Jeroen Broks, 2020, 2021, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 21.05.21
// End License Information

#define mother_boss

#MKL_Version "Star Story - Prologue_Yaqirpa.neil" "21.05.21"
#MKL_Lic     "Star Story - Prologue_Yaqirpa.neil" "Phantasar Closed License"

#use "Script/Use/Anyway"
#use "Script/MapScript/Yaqirpa_Warp"



group MScript
	readonly delegate NPC_SAVE1 = MSave.ToSave
	readonly delegate NPC_SAVE2 = MSave.ToSave
	readonly delegate FTL = {"PLAYER","FCrystal","FBriggs"}
	readonly var YW = Yaqirpa_Warp
	
	get bool Sleutel
		return gv.gBool.Prologue_Yaqirpa_Sleutel
	end
	set bool Sleutel
		gv.gBool.Prologue_Yaqirpa_Sleutel = value
	end
	
	void NPC_Sleutel()
		Player.WalkTo("SPOT_Sleutel")
		MapText("KEYTOTOWER")
		Sleutel = true
		Map.Kill("NPC_Sleutel",true)
	end
	
	void NPC_Deur()
		if Sleutel && (!Done.Prologue_Yaqirpa_Deur_Opened)
			Map.Kill("NPC_DEUR")
		end
	end
	
	void GoToStart()
		Map.SpawnPlayer("Start")
		Map.SetObjField("PLAYER","Texture","GFX/Actors/Uniform/Wendicka.North.jpbf")
		Spawn.Reset()
	end
	
	void Entrance_Zone()
		CSay("Entrance Zone Event")
		Map.HideButLabel("Entrance")
		Map.ShowPlayer()
	end
	
	void Enter_Save()
		Map.HideButLabel("save")
		Map.ShowPlayer()
		if !Done.Prologue_Yaqirpa_Explained_About_Diary_At_The_Start_By_Briggs
			MapText("EXPLAIN_SAVE")
		end
	end
	
	void FightAstro()
		StartCombat.Prepare()
		StartCombat.Foe("Reg/Astrilopup",math.random(__skill,(2*__skill)+1))
		StartCombat.Foe("Reg/Astrilopup",math.random(__skill,(2*__skill)+2))
		StartCombat.Tune = StartCombat.RandomTune
		StartCombat.Boss = false
		StartCombat.Arena = "Yaqirpa.png"	
		StartCombat.Begin()
	end
		
	void Enter_Kantoor()
		Map.HideButLabel("Kantoor")
		Map.ShowPlayer()
		// TODO: Astrilopup encounter
		if !gv.gBool.Prologue_Yaqirpa_AstrilopupEncounter_Floor1
			MapText("EAVESDROP_ASTRILOPUP_01")
			
			FightAstro()
		end
	end
	
	void Astrilopups_Top()
		if !gv.gBool.Prologue_Yaqirpa_Astrilotop
			MapText("ASTRILOATTACK")
			Schedule.Add("After_TopAstrilo")
			FightAstro()
		end
	end
	
	void After_TopAstrilo()
		Map.Kill("Astro3",true)
		Map.Kill("Astro4",true)
		gv.gBool.Prologue_Yaqirpa_Astrilotop=true
	end
	
	
	void After_KantoorAstrilo()
		Map.Kill("Astro1",true)
		Map.Kill("Astro2",true)
		MapText("EAVESDROP_ASTRILOPUP_02")
		gv.gBool.Prologue_Yaqirpa_AstrilopupEncounter_Floor1=true
	end
	
	void PostBoss()
		Map.ObjHide("FBriggs")
		MapText("POSTBOSS_START")
		Map.ObjHide("FCrystal")
		Map.ObjHide("FBriggs")
		Map.ObjShow("DeadCrystal")
		RPGParty[2] = ""
		MapText("POSTBOSS_WENDICKARAGE")
		var explosion = Image.Load("GFX/Scenario/Explosion.png")
		var sex = Audio.Load("Audio/Scenario/Explosion.ogg")
		Graphics.Cls()
		sex.Play()
		for i=0,255
			__white
			explosion.Stretch(0,0,Screen.W,Screen.H)
			Graphics.SetAlpha(i)
			Graphics.Rect(0,0,Screen.W,Screen.H)
			Graphics.SetAlpha(255)
			Graphics.Flip()
			if i==0
				FPS.Sleep(2000)
			else
				FPS.Sleep(1)
			end
		end
		CSay("Time to go to sickbay")
		Map.Load("Excalibur - Sickbay")
		Schedule.Add("Sickbay")
		//Lua.error("Post Boss not yet scripted")
	end
	
	void Boss()
		Map.ObjWalkTo("FBriggs","PreBossBriggs1")
		MapText("BOSS_START")
		Map.ObjWalkTo("FBriggs","PreBossBriggs2")
		Map.ObjSet("FBriggs","X",-20000)
		Map.ObjHide("FBriggs")
		Map.ObjShow("DeadBriggs")
		For i=1760,1600,-4
			Graphics.Cls()
			Map.ObjSet("DeadBriggs","Y",i)
			Map.DrawScreen()
			Graphics.Flip()
		End
		MapText("BOSS_SHOUTBRIGGS")
		MapText("BOSS_BRIGGSISDEAD")
		Player.WalkTo("BossWendicka")
		Map.ObjWalkTo("FCrystal","BossCrystal")		
		Map.HideButLabel("Boss")
		Map.ShowPlayer()
		Map.ObjShow("FCrystal")
		MapText("BOSS_COMMENCE")
		RPGParty[3]=""
		RPGChar.Kill("Briggs")
		Schedule.Add("PostBoss")
		#if mother_boss
			StartCombat.Prepare()
			StartCombat.Foe("Boss/BrainDroid",Caps.LvCycle(__skill+9,200,500,750,1000,2500,5000,2500*__skill,3000*__skill,4000*__skill,10000))
			StartCombat.Tune = StartCombat.RandomBossTune
			StartCombat.Boss = true
			StartCombat.Arena = "Yaqirpa.png"	
			StartCombat.Begin()		
		#fi
	end
	
	void TutEnemy()
		Map.HideButLabel("GreatHall")
		Map.ShowPlayer()
		if !Done.Prologue_Yaqirpa_Explained_About_Foes_At_The_Start_By_Briggs
			MapText("EXPLAIN_FOES")
		end
	end
	
	void Follow(int i,string act)
		if !Map.HasTag(act)
			//Map.Spawn(act,FTL[i-1])
			Map.SpawnXY(act,Map.ObjInt(FTL[i-1],"X"),Map.ObjInt(FTL[i-1],"Y"))
			CSayF("Spawned %s on %s",act,FTL[i-1])		
		end
		Map.ObjSet(act,"Texture",sprintf("GFX/Actors/Uniform/%s.%s.jpbf",right(act,#act-1),Map.ObjStr(act,"Wind")))
		Map.ObjSet(act,"Visible",true)
		Map.ObjSet(act,"Impassible",false)
		//if Map.ObjBool(act,"Walking")
		//	return
		//end
		string Tag1 = FTL[i-1]
		int X1 = Map.ObjInt(Tag1,"X")
		int Y1 = Map.ObjInt(Tag1,"Y")
		int X2 = Map.ObjInt(act, "X")
		int Y2 = Map.ObjInt(act, "Y")
		int hsiz = math.abs(X1-X2)
		int vsiz = math.abs(Y1-Y2)
		int distance = math.floor(math.sqrt( (hsiz*hsiz) + (vsiz*vsiz) ))
		bool walking = Map.ObjBool(act,"Walking")
		bool ShouldWalk = (distance>96) && (!walking)
		// CSayF("Leader %s (%4d,%4d) %s -- Followed by: %s, distance: %4d!",Tag1,X1,Y1,Map.ObjStr(Tag1,"Wind"),act,distance)
		// CSayF("Follower %s (%4d,%4d) walking: %s -- Should walk: %s (%s)",act,X2,Y2,tostring(Map.ObjBool(act,"Walking")),tostring(shouldwalk),lua.type(shouldwalk))
		// CSayF("ShouldWalk = %s, because distance>96 = %s and walking = %s",tostring(ShouldWalk),tostring(distance>96),tostring(walking))
		if ShouldWalk
			switch Map.ObjStr(Tag1,"Wind"):upper()
				case "NORTH"
					Map.ObjWalkTo(act,X1,Y1+64)
				case "SOUTH"
					Map.ObjWalkTo(act,X1,Y1-64)
				case "WEST"
					Map.ObjWalkTo(act,X1+64,Y1)
				case "EAST"
					Map.ObjWalkTo(act,X1-64,Y1)
				default
					CSayF("Unknown wind: %s",Map.ObjStr(Tag1,"Wind"))
			end
		end
	end

	void Update()
		Follow(2,"FCrystal")
		Follow(3,"FBriggs")
	end

	void Draw()
		static var ikey = Image.Load("GFX/Textures/Things/Key.png")
		if Sleutel
			ikey.Draw(Screen.W-200,50)
		end
	end
	
	void Load()
	end
	
	void SetZA()
		ZA.Enter("Entrance_Zone","Entrance_Zone")
		ZA.Enter("EnterSave","Enter_Save")
		ZA.Enter("SAVEZONE1","Enter_Save")
		ZA.Enter("Kantoor","Enter_Kantoor")
		ZA.Enter("TutEnemy","TutEnemy")			
		ZA.Enter("Astrilopups","Astrilopups_Top")
		Yaqirpa_Warp.StartUp()
	end

	CONSTRUCTOR
		YW.Prologue = true
		CSay("Welcome to the Yaqirpa! Let the quest begin!")
		Map.AutoScroll = true
		// No transporter pads in the prologue
		// (I can do this safely as the place you get back to is aliased, to prevent conflicts as the prologue differs too much in setup from the rest of the game).
		for NoTrans in each {"","Top"}
			CSayF("Removing transpad: %s",NoTrans)
			Map.Kill(sprintf("Trans.Pad.Yaqirpa%s",NoTrans))
		end
	end

end

init
	CSay("Yaqirpa Init")
	MScript.SetZA()
end