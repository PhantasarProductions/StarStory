// License:
// 	  src/Script/Script/Flow/FlowCombat.ScyndiBundle/Combat_Execution.scyndi
// 
// 	  Version: 25.01.16 XIII
// 	
// 	  Copyright (C) 2021, 2022, 2025 Jeroen Petrus Broks
// 	
// 	  ===========================
// 	  This file is part of a project related to the Phantasar Chronicles or another
// 	  series or saga which is property of Jeroen P. Broks.
// 	  This means that it may contain references to a story-line plus characters
// 	  which are property of Jeroen Broks. These references may only be distributed
// 	  along with an unmodified version of the game.
// 
// 	  As soon as you remove or replace ALL references to the storyline or character
// 	  references, or any termology specifically set up for the Phantasar universe,
// 	  or any other univers a story of Jeroen P. Broks is set up for,
// 	  the restrictions of this file are removed and will automatically become
// 	  zLib licensed (see below).
// 
// 	  Please note that doing so counts as a modification and must be marked as such
// 	  in accordance to the zLib license.
// 	  ===========================
// 
// 
// 	  zLib license terms:
// 	
// 	  This software is provided 'as-is', without any express or implied
// 	  warranty.  In no event will the authors be held liable for any damages
// 	  arising from the use of this software.
// 
// 	  Permission is granted to anyone to use this software for any purpose,
// 	  including commercial applications, and to alter it and redistribute it
// 	  freely, subject to the following restrictions:
// 	
// 	  1. The origin of this software must not be misrepresented; you must not
// 	     claim that you wrote the original software. If you use this software
// 	     in a product, an acknowledgment in the product documentation would be
// 	     appreciated but is not required.
// 	  2. Altered source versions must be plainly marked as such, and must not be
// 	     misrepresented as being the original software.
// 	  3. This notice may not be removed or altered from any source distribution.
// End License


Module

#use "Libs/SCI/Sys"
#use "Libs/SCI/Audio"
#use "Libs/SCI/Graphics"

#use "Script/Use/Link"
#use "Script/Use/Anyway"
#use "Script/Use/Convert/ViesBah"

#macro _Announce 0
#macro _Stance 1
#macro _SpellAni 2
#macro _AblEffect 3
#macro _AblCharge 4

LocExtern Stage "Scyndi.Class.Stage"
LocExtern Zelf "Scyndi.Class.Combat_Execution"

	var Act
	int MyStage = _Announce
	var Targets
	int AnnounceCD
	
	string AnnounceText
	int AnnounceX
	var AnnounceIcon
	var AnnouncePortrait
	readonly var AnnounceFont = ObtainFont("BIGTEXT") 
	
	get var Executor
		return Act.Executor
	end
	
	void Fini()
		// This "Nil" definitions are to make sure some crucial data won't 
		// linger causing the garbage collector to bug.
		// Except maybe for 'Stage.Current' as that is just the result to the "IDLE" stage in the general combat flow.
		Act = Nil
		Stage.Current = Nil
		Targets = Nil
		AnnounceIcon = Nil
		AnnouncePortrait = Nil
	end
	
	void Reject(string Reason)
		static var Buzz = Audio.Obtain("BUZZ")
		Buzz.Play()
		Act.Executor.Hurt("Action Rejected!",255,0,0)
		Act.Executor.Hurt(Reason,255,0,0)
		CSayF("Action reject: %s",Reason)
		switch __skill
			case 1
				Act.Executor.Gauge=0
				if Act.Executor.Team=="Hero"
					Act.Executor.Gauge = 7500
				end
			case 2
				Act.Executor.Gauge=5000
			case 3
				if Act.Executor.Team=="Foe"
					Act.Executor.Gauge = 7500
				end
		end
		Stage.Current = nil
	end
	
	void CleanTargets()
		// Remove targets that have become invalid by the time ACT was reached.
		Targets = {}
		// pairs and not ipairs. I must make sure ALL targets are seen. The order doesn't matter
		for i,tag in pairs(Act.Targets_Tag)
			var tar = Act.Targets[i]
			if (!tar)
				//Dev.CWrite(sprintf("DA FUCK! Target '%s' on index %04d is nil?",tag,i),255,0,0)
				CSayF("DA FUCK! Target '%s' on index %04d is nil?",tag,i)
			elseif tar.Available && ( (Executor.Team=="Hero" && tar.Targetable_Hero) || (Executor.Team=="Foe" && tar.Targetable_Foe))
				Targets += tar
			end
		end
		// Stuff cleaned to prevent issues with SpellAnis and stuff
		// Act.Targets_Tag = {}
		for i,dat in ipairs(Targets)
			Act.Targets_Tag[i] = dat.Tag
			Act.Targets_Tag[i+1] = nil
		end
	end
	
	get int CountTargets
		return len(Targets)
	end
	
	void ComeToMe(SetAct)
		Party.AutoBuffExpire()
		Act = SetAct
		Stage.Current = zelf
		CleanTargets()
		MyStage = _Announce
		If Act.Executor.HP<=0
			Reject("Dead")
			Return
		End
		if CountTargets<=0
			Reject("No more valid targets")
			return
		end
		if Act.Learning and Executor.Team=="Hero"			
			// Announce learning by BoxText
			BoxText.Serial("COMBAT","NEWABILITY."..Act.Executor.Tag,"FLOW_COMBAT")
			// TODO: Instantly cure blocking statuses to make sure the move is executed legally
			Act.Executor.Char.Points.EXP.Have += 1000
		end
		if Act.UseCommonAmmo
			if Executor.Ammo<=0
				Reject("Out of Ammo")
				return
			end
			Executor.Ammo--
		elseif Act.BurnAP>0
			if Executor.AP<Act.BurnAP
				Reject("Not enough AP")
				return
			end
			Executor.AP -= Act.BurnAP
		elseif Act.UseARMAmmo
			If Executor.Tag!="Crystal"
				Lua.error("ARM used, but not by Crystal!")
			end
			string PA := right(Act.ActTag,len(Act.ActTag)-4)
			var PNT := Executor.Char.Points["ARM.AMMO."..PA]
			if PNT.Have<=0
				Reject("Out of ammo")
				return
			end
			PNT.Have--			
		elseif Act.ConsumeItem>=0
			string socktag = sprintf("INVAMNT%d",Act.ConsumeItem)
			if Act.Executor.Char.Stat[socktag]<=0
				CSayF("%s: Socket %s appears to be set to %d and counts as empty!",Act.Executor.Tag,socktag,Act.Executor.Char.Stat[socktag])
				Reject("Empty inventory socket")
				return
			end
			Act.Executor.Char.Stat[socktag]--
			JwlRecalc(Act.Executor.Tag)
		end
		AnnounceCD = 123
		AnnounceText = Act.Act.Name
		AnnounceX = (Screen.W div 2) - (AnnounceFont.TextWidth(AnnounceText) div 2)
		if Act.Act.Icon 
			//&& Act.Act.Icon!=""
			AnnounceIcon = Act.Act.Icon //= Image.LoadNew(Act.Act.Icon,"ABL_ICON_"..Act.Act.Icon)
		end
		if Executor.Team=="Hero"			
			AnnouncePortrait = Image.Obtain("PORTRET_"..Executor.Tag)
		elseif Executor.Name=="ExHuRU"
			AnnouncePortrait = Image.LoadNew("GFX/Portret/ExHuRU/General.png","PORTRET_ExHuRU")
			AnnouncePortrait.HotBottomCenter()
		elseif Executor.Name=="The Goddess"
			AnnouncePortrait = Image.LoadNew("GFX/Portret/Goddess/General.png","PORTRET_Goddess")
			AnnouncePortrait.HotBottomCenter()
		end
	end

	void Draw()
		switch MyStage
			case _Announce _AblEffect _AblCharge
				// Do Nothing at all.
			case _Stance
				// Handled elsewhere
			case _SpellAni
				__White
				Act.SpellAni.Draw()
			default
				Lua.error("Combat.Execution.Draw: Unknown stage ("..MyStage..")")
		end
		__White
		if AnnouncePortrait
			AnnouncePortrait.Draw(AnnounceX-100,200)
		end
		AnnounceIcon.Draw(AnnounceX-25,200)
		Graphics.Color(0,180,255)
		AnnounceFont.DarkText(AnnounceText,AnnounceX,200,0,1)
	end
	
	string upper(string s)
		return s:upper()
	end
	
	Void HeroReward()
		If Act.Executor.Team=="Hero" && Prefixed(Act.ActTag,"ABL_")
			ablused[Executor.Tag]++
			gv.gInt["ABL.USED."..upper(Act.Executor.Tag).."."..upper(Act.ActTag) ]++
			String ShActTag := right(Act.ActTag,len(Act.ActTag)-4)
			If Act.ActivatePowerUp!="" && (!Act.Executor.Char.List.ABL_POWERUP.Has(ShActTag.."."..Act.ActivatePowerUp))
				Act.Executor.Hurt("Activated Powerup: "..Act.ActivatePowerUp)
				Act.Executor.Char.List.ABL_POWERUP.Add = ShActTag.."."..Act.ActivatePowerUp
			End
			If Act.Learning
				Act.Executor.Char.List.ABL.Add = Act.LearningABL
				Act.Executor.Hurt("Learned: "..Act.Act.Name)
			End
		End
	End
	
	void Update()
		switch MyStage
			case _Announce
				AnnounceCD--
				if AnnounceCD<=0
					MyStage = _Stance
				end
			case _Stance
				if Executor.Pose(Act)
					MyStage = _SpellAni
					if Prefixed(Act.ActTag,"ARM_")
						static var Crystal_GunShot = Audio.Obtain("CRYSTAL_ARM")
						Crystal_GunShot.Play()
					end					
					if Act.SpellAni
						Act.SpellAni.Start(Act)
					else
						CSay("No spellani, so let's skip to the effect then!")
						MyStage = _AblEffect
					end
				end
			case _SpellAni
				Act.SpellAni.Update()
				if Act.SpellAni.Ended
					MyStage = _AblEffect
				end
			case _AblEffect
				if Act.Act.UserNextMove && Act.Act.UserNextMove!=""
					MyStage = _AblCharge
					Return
				end
				// The true effect at last!
				CG_Effect.EffectAll(Act)
				Act.AllUpAP()
				if Act.Executor.Team=="Hero"
					Act.Executor.UpAP()
				end
				Act.Executor.NextAct=nil
				Act.Executor.Gauge=0
				if (Act.Executor.Team=="Hero")
					Act.Executor.State="IDLE"
				end
				HeroReward()
				AnnouncePortrait = nil
				Stage.Current = nil		
			case _AblCharge
				Var Exe := Act.Executor
				Var CAc := New Class_Action(Exe.Tag,Act.Act.UserNextMove)
				Exe.ChargedAct = CAc
				For i,tag in iPairs(Act.Targets_Tag)
					Cac.Targets_Tag[i] = tag
				End
				Act.Executor.Hurt("Charged",180,0,255)
				AnnouncePortrait     = nil
				Stage.Current        = nil
				Act.Executor.NextAct = nil
				Exe.Gauge            = 0
				if (Act.Executor.Team=="Hero")
					Act.Executor.State="IDLE"
				end
				HeroReward()
			default
				Fini()
		end
	end

//End
