// License Information:
// **********************************************
// 
// src/Tricky Script/Script/Flow/FlowJukebox.neil
// (c) Jeroen Broks, 2021, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 21.05.28
// End License Information

#use "Libs/Music"
#use "Script/Data/General/JukeData"
#use "Script/Use/Anyway"



ReadOnly Var Mouse = Image.Obtain("MOUSE")
ReadOnly Var Font  = ImageFont.Obtain("BOXTEXT")
ReadOnly Var Head  = ImageFont.Obtain("BIGTEXT")
ReadOnly Var FSys  = ImageFont.Obtain("SYSFONT")

int ReturnTo = 0
global const int ReturnMain  = 1
global const int ReturnField = 2

Var Playing = nil


#region ExtraItemsClass
Class XItems
	Static Var Lijst = new TLinkedList()
	Static Int SY = 0
	ReadOnly Int _Y
	ReadOnly Delegate Action
	ReadOnly Delegate Allowed
	ReadOnly String Caption
	
	Get Int Y
		Return Screen.H - 30 - _Y
	End
	
	Constructor(string AC,Delegate Act,Delegate Allw)
		Action  = Act
		Allowed = Allw
		Caption = AC
		_Y = SY
		SY += 30
	End
End
Init
	// Quit
	Bool Always()
		Return True
	End
	Void Quit()
		Switch ReturnTo
			Case 1
				Flow.Current="MAINMENU"
				Music.Play("Music/Introduction/Introduction.ogg")
			Case 2
				Flow.Current="FIELD"
				MapScript.MapMusic()
			Default
				Lua.Error("Unknown Return!")
		End
	End
	XItems.Lijst.AddLast( new XItems("Back",Quit,Always) )
	
	// Set to Hawk
	Bool AlHawk()
		Return ReturnTo==2 && Playing!=nil && gv.gstring.Hawk_Music!=Playing.File
	End
	Void StHawk()
		gv.gstring.Hawk_Music=Playing.File
	End
	XItems.Lijst.AddLast(new XItems("Set as Hawk tune",StHawk,AlHawk) )
	
	// Website
	Void GoSite()
		OpenURL(Playing.Site)
	End
	Bool AlSite()
		Return Playing!=nil && Playing.Site!=""
	End
	XItems.Lijst.AddLast(new XItems("Visit artist's website",GoSite,AlSite) )
End	
#endregion



Init
	Lua.ReturnMain  = ReturnMain
	Lua.ReturnField = ReturnField
End

Table License

Void Play(Track)
	Playing = Track
	Music.Play(Track.File)
	License = {}
	Table LicChop = Split(Playing.License," ")
	Int i = 0
	For W in Each(LicChop)
		If !License[i]
			License[i]=W
		Else
			if FSys.TextWidth(License[i].." "..W)<Screen.W Div 2
				License[i]+=" "..W
			Else
				i++
				License[i]=W
			End
		End
	End
End

Global Void Arrival(int i)
	ReturnTo = i
End

Global Void Apollo_Flow()
	Static Int Scroll = 0
	Int Y = 40 - Scroll
	Int MX = Events.MouseX()
	Int MY = Events.MouseY()
	Bool ML = Events.MouseHitLeft()
	// Header
	__Red
	Head.Text("Jukebox",Screen.W Div 4,0,2,0)
	// Track list
	For JukeTrack,JukeLink In JukeData.Lijst.Each
		If Y>40 && Y<Screen.H-40
			__Ember
			If MX<(Screen.W div 2)-32 && MY>Y && MY<Y+25
				__Yellow
				IF ML
					Play(JukeTrack)
				END
			End	
			Font.Text(JukeTrack.Title,5,Y)
		End
		Y+=25
	End
	// Track Info
	If Playing
		Static Int X = Screen.W Div 2
		__SkyBlue
		Head.Text("Playing:",X,40)
		Head.Text("Artist:",X,100)
		Head.Text("License:",X,160)
		__Ember
		Font.Text(Playing.Title,X,75)
		Font.Text(Playing.Artist,X,135)
		//FSys.Text(Playing.License,X,195)
		For i,l In Pairs(License)
			FSys.Text(l,X,195+(i*25))
		End
	End
	// Extra Menu
	For XI in XItems.Lijst.Each
		If XI.Allowed()
			__SkyBlue
			If MX>Screen.W Div 2 && MY>XI.Y && MY<XI.Y+30
				__Cyan
				If ML
					XI.Action()
				End
			End
			Font.Text(XI.Caption,(Screen.W div 4) * 3,XI.Y,2,0)
		End
	End
	// Mouse
	__White
	Mouse.Draw(MX,MY)
	// debug
	Dev.ToConsole()
End