// License Information:
// src/Tricky Script/Script/Subs/SubAchievements.neil
// Version: 21.06.21
// Copyright (C) 2021 Jeroen Petrus Broks
// 
// ===========================
// This file is part of a project related to the Phantasar Chronicles or another
// series or saga which is property of Jeroen P. Broks.
// This means that it may contain references to a story-line plus characters
// which are property of Jeroen Broks. These references may only be distributed
// along with an unmodified version of the game.
// 
// As soon as you remove or replace ALL references to the storyline or character
// references, or any termology specifically set up for the Phantasar universe,
// or any other univers a story of Jeroen P. Broks is set up for,
// the restrictions of this file are removed and will automatically become
// zLib licensed (see below).
// 
// Please note that doing so counts as a modification and must be marked as such
// in accordance to the zLib license.
// ===========================
// zLib license terms:
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information

#use "Script/Use/Anyway"

group SubAch

	table Awarded = {}
	readonly var Data = NeilUse("Script/Data/General/Achievements")

	Void Award(String a)
		a = a:upper()
		If !Data[a]
			Lua.error("Non-existent achievement: "..a)
		End
		If Awarded[a]
			Return
		End
		Awarded[a] = True
		Schedule.Add("MSG:You earned the achievement: "..Data[a].Title)
	End
	
	void Save()
		string str = ""
		for k,v in spairs(Awarded)
			if v
				if str!=""
					str += ", "
				end
				str += sprintf("\"%s\"",k)
			end
		end
		SaveGame.Data["ACHIEVEMENTS.LUA"] = sprintf("return {%s}",str)
	end
	
	void Load()
		delegate L
		Awarded = {}
		if !SaveGame.HasData("ACHIEVEMENTS.LUA")
			return
		end
		L = Lua.assert(Lua.load(SaveGame.Data["ACHIEVEMENTS.LUA"],"SG:Achievements.Lua"))
		table LA = L()
		for k in each(LA)
			Awarded[k:upper()]=true
		end
	end
	
	Void List(Int SX,Int SY,Int EX, Int EY)
		Static Int H = 40
		Static Int Scroll = 0
		Static Var CFnt = ImageFont.Obtain("BOXTEXT")
		Static Var DFnt = ImageFont.Obtain("SYSFONT")
		Int Y = SY-Scroll
		//CSayF("Rukkie fukkie: (%d,%d)->(%d,%d)",SX,SY,EX,EY)
		For K,A in sPairs(Data)
			Bool Show
			If Y>=SY && Y<=EY-H
				Switch A.Show
					Case "Always" 
						Show = true
					Case "Achieved"
						Show = Awarded[K]==true
					Default
						Lua.error("Unknown trophy Show Type")
				End
				If Awarded[K]
					__SkyBlue
				Else
					Graphics.Color(0,100,180)
				End
				If Show
					CFnt.Text(A.Title,SX+20,Y)
					DFnt.Text(A.Description,SX+25,Y+22)
				Else
					CFnt.Text("????",SX+20,Y)
				End
				If Y+(H*2)<=EY
					__Ember
					Graphics.Line(SX,Y+(H),EX-40,Y+(H))
				End
			End
			Y+=H			
		End
		// Scroll
		Static Var ButtonUp = Image.Obtain("UP")
		Static Var ButtonDown = Image.Obtain("DOWN")
		Int  MX = Events.MouseX()
		Int  MY = Events.MouseY()
		Bool ML = Events.MouseHitLeft()
		If Scroll!=0
			If MY>SY && MY<SY+32 && MX>EX-65 && MX<EX
				__Ember
				If ML
					Scroll-=H
				End
			Else
				Graphics.Color(180,100,0)
			End
			ButtonUp.Draw(EX-65,SY)
		End
		If Y>EY
			If MY<EY && MY>EY-32 && MX>EX-65 && MX<EX
				__Ember
				If ML
					Scroll+=H
				End
			Else
				Graphics.Color(180,100,0)
			End
			ButtonDown.Draw(EX-65,EY-35)
		End
	End
End